/-
Copyright (c) 2021 Luke Kershaw. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luke Kershaw, Jo√´l Riou
-/
import category_theory.preadditive.additive_functor
import category_theory.triangulated.basic

/-!
# Rotate

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file adds the ability to rotate triangles and triangle morphisms.
It also shows that rotation gives an equivalence on the category of triangles.

-/

noncomputable theory

open category_theory
open category_theory.preadditive
open category_theory.limits

universes v v‚ÇÄ v‚ÇÅ v‚ÇÇ u u‚ÇÄ u‚ÇÅ u‚ÇÇ

namespace category_theory.pretriangulated
open category_theory.category

/--
We work in an preadditive category `C` equipped with an additive shift.
-/
variables {C : Type u} [category.{v} C] [preadditive C]
variables [has_shift C ‚Ñ§]

variables (X : C)

/--
If you rotate a triangle, you get another triangle.
Given a triangle of the form:
```
      f       g       h
  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ> X‚ü¶1‚üß
```
applying `rotate` gives a triangle of the form:
```
      g       h        -f‚ü¶1‚üß'
  Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ>  X‚ü¶1‚üß ‚îÄ‚îÄ‚îÄ> Y‚ü¶1‚üß
```
-/
@[simps]
def triangle.rotate (T : triangle C) : triangle C := triangle.mk T.mor‚ÇÇ T.mor‚ÇÉ (-T.mor‚ÇÅ‚ü¶1‚üß')

section

/--
Given a triangle of the form:
```
      f       g       h
  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ> X‚ü¶1‚üß
```
applying `inv_rotate` gives a triangle that can be thought of as:
```
        -h‚ü¶-1‚üß'     f       g
  Z‚ü¶-1‚üß  ‚îÄ‚îÄ‚îÄ>  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z
```
(note that this diagram doesn't technically fit the definition of triangle, as `Z‚ü¶-1‚üß‚ü¶1‚üß` is
not necessarily equal to `Z`, but it is isomorphic, by the `counit_iso` of `shift C`)
-/
@[simps]
def triangle.inv_rotate (T : triangle C) : triangle C :=
triangle.mk (-T.mor‚ÇÉ‚ü¶(-1:‚Ñ§)‚üß' ‚â´ (shift_shift_neg _ _).hom) T.mor‚ÇÅ
  (T.mor‚ÇÇ ‚â´ (shift_neg_shift _ _).inv)

end

variables (C)

/--
Rotating triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def rotate : triangle C ‚•§ triangle C :=
{ obj := triangle.rotate,
  map := Œª T‚ÇÅ T‚ÇÇ f,
  { hom‚ÇÅ := f.hom‚ÇÇ,
    hom‚ÇÇ := f.hom‚ÇÉ,
    hom‚ÇÉ := f.hom‚ÇÅ‚ü¶1‚üß',
    comm‚ÇÉ' := by { dsimp, simp only [comp_neg, neg_comp, ‚Üê functor.map_comp, f.comm‚ÇÅ], }, }, }

/--
The inverse rotation of triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def inv_rotate : triangle C ‚•§ triangle C :=
{ obj := triangle.inv_rotate,
  map := Œª T‚ÇÅ T‚ÇÇ f,
  { hom‚ÇÅ := f.hom‚ÇÉ‚ü¶-1‚üß',
    hom‚ÇÇ := f.hom‚ÇÅ,
    hom‚ÇÉ := f.hom‚ÇÇ,
    comm‚ÇÅ' :=
    begin
      dsimp,
      rw [neg_comp, assoc, comp_neg, neg_inj, ‚Üê functor.map_comp_assoc, ‚Üê f.comm‚ÇÉ,
        functor.map_comp, assoc],
      erw [‚Üê nat_trans.naturality],
      refl,
    end,
    comm‚ÇÉ' := by { dsimp, erw [‚Üê f.comm‚ÇÇ_assoc, assoc, ‚Üê nat_trans.naturality], refl, }, }, }

variables {C}

variables [‚àÄ n : ‚Ñ§, functor.additive (shift_functor C n)]

local attribute [simp] shift_shift_neg' shift_neg_shift'
  shift_shift_functor_comp_iso_id_add_neg_self_inv_app
  shift_shift_functor_comp_iso_id_add_neg_self_hom_app

/-- The unit isomorphism of the auto-equivalence of categories `triangle_rotation C` of
`triangle C` given by the rotation of triangles. -/
@[simps]
def rot_comp_inv_rot : ùü≠ (triangle C) ‚âÖ rotate C ‚ãô inv_rotate C :=
nat_iso.of_components (Œª T, triangle.iso_mk _ _ ((shift_equiv C (1 : ‚Ñ§)).unit_iso.app T.obj‚ÇÅ)
  (iso.refl _) (iso.refl _) (by tidy) (by tidy) (by tidy)) (by tidy)

/-- The counit isomorphism of the auto-equivalence of categories `triangle_rotation C` of
`triangle C` given by the rotation of triangles. -/
@[simps]
def inv_rot_comp_rot : inv_rotate C ‚ãô rotate C ‚âÖ ùü≠ (triangle C) :=
nat_iso.of_components (Œª T, triangle.iso_mk _ _ (iso.refl _) (iso.refl _)
  ((shift_equiv C (1 : ‚Ñ§)).counit_iso.app T.obj‚ÇÉ) (by tidy) (by tidy) (by tidy)) (by tidy)

variable (C)

/--
Rotating triangles gives an auto-equivalence on the category of triangles in `C`.
-/
@[simps]
def triangle_rotation : equivalence (triangle C) (triangle C) :=
{ functor := rotate C,
  inverse := inv_rotate C,
  unit_iso := rot_comp_inv_rot,
  counit_iso := inv_rot_comp_rot, }

variables {C}

instance : is_equivalence (rotate C) :=
by { change is_equivalence (triangle_rotation C).functor, apply_instance, }
instance : is_equivalence (inv_rotate C) :=
by { change is_equivalence (triangle_rotation C).inverse, apply_instance, }

end category_theory.pretriangulated
