/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import algebra.homology.additive
import category_theory.idempotents.karoubi

/-!
# Idempotent completeness and homological complexes

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains simplifications lemmas for categories
`karoubi (homological_complex C c)` and the construction of an equivalence
of categories `karoubi (homological_complex C c) ‚âå homological_complex (karoubi C) c`.

When the category `C` is idempotent complete, it is shown that
`homological_complex (karoubi C) c` is also idempotent complete.

-/

namespace category_theory

open category

variables {C : Type*} [category C] [preadditive C] {Œπ : Type*} {c : complex_shape Œπ}

namespace idempotents

namespace karoubi

namespace homological_complex

variables {P Q : karoubi (homological_complex C c)} (f : P ‚ü∂ Q) (n : Œπ)

@[simp, reassoc]
lemma p_comp_d : P.p.f n ‚â´ f.f.f n = f.f.f n :=
homological_complex.congr_hom (p_comp f) n

@[simp, reassoc]
lemma comp_p_d : f.f.f n ‚â´ Q.p.f n = f.f.f n :=
homological_complex.congr_hom (comp_p f) n

@[reassoc]
lemma p_comm_f : P.p.f n ‚â´ f.f.f n = f.f.f n ‚â´ Q.p.f n :=
homological_complex.congr_hom (p_comm f) n

variable (P)

@[simp, reassoc]
lemma p_idem : P.p.f n ‚â´ P.p.f n = P.p.f n :=
homological_complex.congr_hom P.idem n

end homological_complex

end karoubi

open karoubi

namespace karoubi_homological_complex_equivalence

namespace functor

/-- The functor `karoubi (homological_complex C c) ‚•§ homological_complex (karoubi C) c`,
on objects. -/
@[simps]
def obj (P : karoubi (homological_complex C c)) : homological_complex (karoubi C) c :=
{ X := Œª n, ‚ü®P.X.X n, P.p.f n, by simpa only [homological_complex.comp_f]
    using homological_complex.congr_hom P.idem n‚ü©,
  d := Œª i j,
    { f := P.p.f i ‚â´ P.X.d i j,
      comm := by tidy, },
  shape' := Œª i j hij, by simp only [hom_eq_zero_iff,
    P.X.shape i j hij, limits.comp_zero], }

/-- The functor `karoubi (homological_complex C c) ‚•§ homological_complex (karoubi C) c`,
on morphisms. -/
@[simps]
def map {P Q : karoubi (homological_complex C c)} (f : P ‚ü∂ Q) : obj P ‚ü∂ obj Q :=
{ f:= Œª n,
  { f:= f.f.f n,
    comm := by simp, }, }

end functor

/-- The functor `karoubi (homological_complex C c) ‚•§ homological_complex (karoubi C) c`. -/
@[simps]
def functor : karoubi (homological_complex C c) ‚•§ homological_complex (karoubi C) c :=
{ obj := functor.obj,
  map := Œª P Q f, functor.map f, }

namespace inverse

/-- The functor `homological_complex (karoubi C) c ‚•§ karoubi (homological_complex C c)`,
on objects -/
@[simps]
def obj (K : homological_complex (karoubi C) c) : karoubi (homological_complex C c) :=
{ X :=
  { X := Œª n, (K.X n).X,
    d := Œª i j, (K.d i j).f,
    shape' := Œª i j hij, hom_eq_zero_iff.mp (K.shape i j hij),
    d_comp_d' := Œª i j k hij hjk, by { simpa only [comp_f]
      using hom_eq_zero_iff.mp (K.d_comp_d i j k), }, },
  p :=
    { f := Œª n, (K.X n).p,
      comm' := by simp, },
  idem := by tidy, }

/-- The functor `homological_complex (karoubi C) c ‚•§ karoubi (homological_complex C c)`,
on morphisms -/
@[simps]
def map {K L : homological_complex (karoubi C) c} (f : K ‚ü∂ L) : obj K ‚ü∂ obj L :=
{ f:=
  { f := Œª n, (f.f n).f,
    comm' := Œª i j hij, by simpa only [comp_f]
      using hom_ext.mp (f.comm' i j hij), },
  comm := by tidy, }

end inverse

/-- The functor `homological_complex (karoubi C) c ‚•§ karoubi (homological_complex C c)`. -/
@[simps]
def inverse :
  homological_complex (karoubi C) c ‚•§ karoubi (homological_complex C c) :=
{ obj := inverse.obj,
  map := Œª K L f, inverse.map f, }


/-- The counit isomorphism of the equivalence
`karoubi (homological_complex C c) ‚âå homological_complex (karoubi C) c`. -/
@[simps]
def counit_iso : inverse ‚ãô functor ‚âÖ ùü≠ (homological_complex (karoubi C) c) :=
eq_to_iso (functor.ext (Œª P, homological_complex.ext (by tidy) (by tidy)) (by tidy))

/-- The unit isomorphism of the equivalence
`karoubi (homological_complex C c) ‚âå homological_complex (karoubi C) c`. -/
@[simps]
def unit_iso : ùü≠ (karoubi (homological_complex C c)) ‚âÖ functor ‚ãô inverse :=
{ hom :=
  { app := Œª P,
    { f :=
      { f := Œª n, P.p.f n,
        comm' := Œª i j hij, begin
          dsimp,
          simp only [homological_complex.hom.comm, homological_complex.hom.comm_assoc,
            homological_complex.p_idem],
        end },
      comm := by { ext n, dsimp, simp only [homological_complex.p_idem], }, },
    naturality' := Œª P Q œÜ, begin
      ext,
      dsimp,
      simp only [comp_f, homological_complex.comp_f, homological_complex.comp_p_d,
        inverse.map_f_f, functor.map_f_f, homological_complex.p_comp_d],
    end, },
  inv :=
  { app := Œª P,
    { f :=
      { f := Œª n, P.p.f n,
        comm' := Œª i j hij, begin
          dsimp,
          simp only [homological_complex.hom.comm, assoc, homological_complex.p_idem],
        end },
      comm := by { ext n, dsimp, simp only [homological_complex.p_idem], }, },
    naturality' := Œª P Q œÜ, begin
      ext,
      dsimp,
      simp only [comp_f, homological_complex.comp_f, inverse.map_f_f, functor.map_f_f,
        homological_complex.comp_p_d, homological_complex.p_comp_d],
    end, },
  hom_inv_id' := begin
    ext,
    dsimp,
    simp only [homological_complex.p_idem, comp_f, homological_complex.comp_f, id_eq],
  end,
  inv_hom_id' := begin
    ext,
    dsimp,
    simp only [homological_complex.p_idem, comp_f, homological_complex.comp_f, id_eq,
      inverse.obj_p_f, functor.obj_X_p],
  end, }

end karoubi_homological_complex_equivalence

variables (C) (c)

/-- The equivalence `karoubi (homological_complex C c) ‚âå homological_complex (karoubi C) c`. -/
@[simps]
def karoubi_homological_complex_equivalence :
  karoubi (homological_complex C c) ‚âå homological_complex (karoubi C) c :=
{ functor    := karoubi_homological_complex_equivalence.functor,
  inverse    := karoubi_homological_complex_equivalence.inverse,
  unit_iso   := karoubi_homological_complex_equivalence.unit_iso,
  counit_iso := karoubi_homological_complex_equivalence.counit_iso, }

variables (Œ± : Type*) [add_right_cancel_semigroup Œ±] [has_one Œ±]

/-- The equivalence `karoubi (chain_complex C Œ±) ‚âå chain_complex (karoubi C) Œ±`. -/
@[simps]
def karoubi_chain_complex_equivalence :
  karoubi (chain_complex C Œ±) ‚âå chain_complex (karoubi C) Œ± :=
karoubi_homological_complex_equivalence C (complex_shape.down Œ±)

/-- The equivalence `karoubi (cochain_complex C Œ±) ‚âå cochain_complex (karoubi C) Œ±`. -/
@[simps]
def karoubi_cochain_complex_equivalence :
  karoubi (cochain_complex C Œ±) ‚âå cochain_complex (karoubi C) Œ± :=
karoubi_homological_complex_equivalence C (complex_shape.up Œ±)

instance [is_idempotent_complete C] : is_idempotent_complete (homological_complex C c) :=
begin
  rw [is_idempotent_complete_iff_of_equivalence
    ((to_karoubi_equivalence C).map_homological_complex c),
    ‚Üê is_idempotent_complete_iff_of_equivalence (karoubi_homological_complex_equivalence C c)],
  apply_instance,
end

end idempotents

end category_theory
