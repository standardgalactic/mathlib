/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Yury Kudryashov
-/
import analysis.convex.normed
import analysis.convex.strict
import analysis.normed.order.basic
import analysis.normed_space.add_torsor
import analysis.normed_space.pointwise
import analysis.normed_space.affine_isometry

/-!
# Strictly convex spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines strictly convex spaces. A normed space is strictly convex if all closed balls are
strictly convex. This does **not** mean that the norm is strictly convex (in fact, it never is).

## Main definitions

`strict_convex_space`: a typeclass saying that a given normed space over a normed linear ordered
field (e.g., `‚Ñù` or `‚Ñö`) is strictly convex. The definition requires strict convexity of a closed
ball of positive radius with center at the origin; strict convexity of any other closed ball follows
from this assumption.

## Main results

In a strictly convex space, we prove

- `strict_convex_closed_ball`: a closed ball is strictly convex.
- `combo_mem_ball_of_ne`, `open_segment_subset_ball_of_ne`, `norm_combo_lt_of_ne`:
  a nontrivial convex combination of two points in a closed ball belong to the corresponding open
  ball;
- `norm_add_lt_of_not_same_ray`, `same_ray_iff_norm_add`, `dist_add_dist_eq_iff`:
  the triangle inequality `dist x y + dist y z ‚â§ dist x z` is a strict inequality unless `y` belongs
  to the segment `[x -[‚Ñù] z]`.
- `isometry.affine_isometry_of_strict_convex_space`: an isometry of `normed_add_torsor`s for real
  normed spaces, strictly convex in the case of the codomain, is an affine isometry.

We also provide several lemmas that can be used as alternative constructors for `strict_convex ‚Ñù E`:

- `strict_convex_space.of_strict_convex_closed_unit_ball`: if `closed_ball (0 : E) 1` is strictly
  convex, then `E` is a strictly convex space;

- `strict_convex_space.of_norm_add`: if `‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ` implies `same_ray ‚Ñù x y` for all
  nonzero `x y : E`, then `E` is a strictly convex space.

## Implementation notes

While the definition is formulated for any normed linear ordered field, most of the lemmas are
formulated only for the case `ùïú = ‚Ñù`.

## Tags

convex, strictly convex
-/

open set metric
open_locale convex pointwise

/-- A *strictly convex space* is a normed space where the closed balls are strictly convex. We only
require balls of positive radius with center at the origin to be strictly convex in the definition,
then prove that any closed ball is strictly convex in `strict_convex_closed_ball` below.

See also `strict_convex_space.of_strict_convex_closed_unit_ball`. -/
class strict_convex_space (ùïú E : Type*) [normed_linear_ordered_field ùïú] [normed_add_comm_group E]
  [normed_space ùïú E] : Prop :=
(strict_convex_closed_ball : ‚àÄ r : ‚Ñù, 0 < r ‚Üí strict_convex ùïú (closed_ball (0 : E) r))

variables (ùïú : Type*) {E : Type*} [normed_linear_ordered_field ùïú]
  [normed_add_comm_group E] [normed_space ùïú E]

/-- A closed ball in a strictly convex space is strictly convex. -/
lemma strict_convex_closed_ball [strict_convex_space ùïú E] (x : E) (r : ‚Ñù) :
  strict_convex ùïú (closed_ball x r) :=
begin
  cases le_or_lt r 0 with hr hr,
  { exact (subsingleton_closed_ball x hr).strict_convex },
  rw ‚Üê vadd_closed_ball_zero,
  exact (strict_convex_space.strict_convex_closed_ball r hr).vadd _,
end

variables [normed_space ‚Ñù E]

/-- A real normed vector space is strictly convex provided that the unit ball is strictly convex. -/
lemma strict_convex_space.of_strict_convex_closed_unit_ball
  [linear_map.compatible_smul E E ùïú ‚Ñù] (h : strict_convex ùïú (closed_ball (0 : E) 1)) :
  strict_convex_space ùïú E :=
‚ü®Œª r hr, by simpa only [smul_closed_unit_ball_of_nonneg hr.le] using h.smul r‚ü©

/-- Strict convexity is equivalent to `‚Äña ‚Ä¢ x + b ‚Ä¢ y‚Äñ < 1` for all `x` and `y` of norm at most `1`
and all strictly positive `a` and `b` such that `a + b = 1`. This lemma shows that it suffices to
check this for points of norm one and some `a`, `b` such that `a + b = 1`. -/
lemma strict_convex_space.of_norm_combo_lt_one
  (h : ‚àÄ x y : E, ‚Äñx‚Äñ = 1 ‚Üí ‚Äñy‚Äñ = 1 ‚Üí x ‚â† y ‚Üí ‚àÉ a b : ‚Ñù, a + b = 1 ‚àß ‚Äña ‚Ä¢ x + b ‚Ä¢ y‚Äñ < 1) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_strict_convex_closed_unit_ball ‚Ñù
    ((convex_closed_ball _ _).strict_convex' $ Œª x hx y hy hne, _),
  rw [interior_closed_ball (0 : E) one_ne_zero, closed_ball_diff_ball, mem_sphere_zero_iff_norm]
    at hx hy,
  rcases h x y hx hy hne with ‚ü®a, b, hab, hlt‚ü©,
  use b,
  rwa [affine_map.line_map_apply_module, interior_closed_ball (0 : E) one_ne_zero,
    mem_ball_zero_iff, sub_eq_iff_eq_add.2 hab.symm]
end

lemma strict_convex_space.of_norm_combo_ne_one
  (h : ‚àÄ x y : E, ‚Äñx‚Äñ = 1 ‚Üí ‚Äñy‚Äñ = 1 ‚Üí x ‚â† y ‚Üí
    ‚àÉ a b : ‚Ñù, 0 ‚â§ a ‚àß 0 ‚â§ b ‚àß a + b = 1 ‚àß ‚Äña ‚Ä¢ x + b ‚Ä¢ y‚Äñ ‚â† 1) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_strict_convex_closed_unit_ball ‚Ñù
    ((convex_closed_ball _ _).strict_convex _),
  simp only [interior_closed_ball _ one_ne_zero, closed_ball_diff_ball, set.pairwise,
    frontier_closed_ball _ one_ne_zero, mem_sphere_zero_iff_norm],
  intros x hx y hy hne,
  rcases h x y hx hy hne with ‚ü®a, b, ha, hb, hab, hne'‚ü©,
  exact ‚ü®_, ‚ü®a, b, ha, hb, hab, rfl‚ü©, mt mem_sphere_zero_iff_norm.1 hne'‚ü©
end

lemma strict_convex_space.of_norm_add_ne_two
  (h : ‚àÄ ‚¶Éx y : E‚¶Ñ, ‚Äñx‚Äñ = 1 ‚Üí ‚Äñy‚Äñ = 1 ‚Üí x ‚â† y ‚Üí ‚Äñx + y‚Äñ ‚â† 2) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_norm_combo_ne_one
    (Œª x y hx hy hne, ‚ü®1/2, 1/2, one_half_pos.le, one_half_pos.le, add_halves _, _‚ü©),
  rw [‚Üê smul_add, norm_smul, real.norm_of_nonneg one_half_pos.le, one_div,
    ‚Üê div_eq_inv_mul, ne.def, div_eq_one_iff_eq (two_ne_zero' ‚Ñù)],
  exact h hx hy hne,
end

lemma strict_convex_space.of_pairwise_sphere_norm_ne_two
  (h : (sphere (0 : E) 1).pairwise $ Œª x y, ‚Äñx + y‚Äñ ‚â† 2) :
  strict_convex_space ‚Ñù E :=
strict_convex_space.of_norm_add_ne_two $ Œª x y hx hy,
  h (mem_sphere_zero_iff_norm.2 hx) (mem_sphere_zero_iff_norm.2 hy)

/-- If `‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ` implies that `x y : E` are in the same ray, then `E` is a strictly
convex space. See also a more -/
lemma strict_convex_space.of_norm_add
  (h : ‚àÄ x y : E, ‚Äñx‚Äñ = 1 ‚Üí ‚Äñy‚Äñ = 1 ‚Üí ‚Äñx + y‚Äñ = 2 ‚Üí same_ray ‚Ñù x y) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_pairwise_sphere_norm_ne_two (Œª x hx y hy, mt $ Œª h‚ÇÇ, _),
  rw mem_sphere_zero_iff_norm at hx hy,
  exact (same_ray_iff_of_norm_eq (hx.trans hy.symm)).1 (h x y hx hy h‚ÇÇ)
end

variables [strict_convex_space ‚Ñù E] {x y z : E} {a b r : ‚Ñù}

/-- If `x ‚â† y` belong to the same closed ball, then a convex combination of `x` and `y` with
positive coefficients belongs to the corresponding open ball. -/
lemma combo_mem_ball_of_ne (hx : x ‚àà closed_ball z r) (hy : y ‚àà closed_ball z r) (hne : x ‚â† y)
  (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) : a ‚Ä¢ x + b ‚Ä¢ y ‚àà ball z r :=
begin
  rcases eq_or_ne r 0 with rfl|hr,
  { rw [closed_ball_zero, mem_singleton_iff] at hx hy,
    exact (hne (hx.trans hy.symm)).elim },
  { simp only [‚Üê interior_closed_ball _ hr] at hx hy ‚ä¢,
    exact strict_convex_closed_ball ‚Ñù z r hx hy hne ha hb hab }
end

/-- If `x ‚â† y` belong to the same closed ball, then the open segment with endpoints `x` and `y` is
included in the corresponding open ball. -/
lemma open_segment_subset_ball_of_ne (hx : x ‚àà closed_ball z r) (hy : y ‚àà closed_ball z r)
  (hne : x ‚â† y) : open_segment ‚Ñù x y ‚äÜ ball z r :=
(open_segment_subset_iff _).2 $ Œª a b, combo_mem_ball_of_ne hx hy hne

/-- If `x` and `y` are two distinct vectors of norm at most `r`, then a convex combination of `x`
and `y` with positive coefficients has norm strictly less than `r`. -/
lemma norm_combo_lt_of_ne (hx : ‚Äñx‚Äñ ‚â§ r) (hy : ‚Äñy‚Äñ ‚â§ r) (hne : x ‚â† y) (ha : 0 < a) (hb : 0 < b)
  (hab : a + b = 1) : ‚Äña ‚Ä¢ x + b ‚Ä¢ y‚Äñ < r :=
begin
  simp only [‚Üê mem_ball_zero_iff, ‚Üê mem_closed_ball_zero_iff] at hx hy ‚ä¢,
  exact combo_mem_ball_of_ne hx hy hne ha hb hab
end

/-- In a strictly convex space, if `x` and `y` are not in the same ray, then `‚Äñx + y‚Äñ < ‚Äñx‚Äñ +
‚Äñy‚Äñ`. -/
lemma norm_add_lt_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : ‚Äñx + y‚Äñ < ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
begin
  simp only [same_ray_iff_inv_norm_smul_eq, not_or_distrib, ‚Üê ne.def] at h,
  rcases h with ‚ü®hx, hy, hne‚ü©,
  rw ‚Üê norm_pos_iff at hx hy,
  have hxy : 0 < ‚Äñx‚Äñ + ‚Äñy‚Äñ := add_pos hx hy,
  have := combo_mem_ball_of_ne (inv_norm_smul_mem_closed_unit_ball x)
    (inv_norm_smul_mem_closed_unit_ball y) hne (div_pos hx hxy) (div_pos hy hxy)
    (by rw [‚Üê add_div, div_self hxy.ne']),
  rwa [mem_ball_zero_iff, div_eq_inv_mul, div_eq_inv_mul, mul_smul, mul_smul,
    smul_inv_smul‚ÇÄ hx.ne', smul_inv_smul‚ÇÄ hy.ne', ‚Üê smul_add, norm_smul,
    real.norm_of_nonneg (inv_pos.2 hxy).le, ‚Üê div_eq_inv_mul, div_lt_one hxy] at this
end

lemma lt_norm_sub_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : ‚Äñx‚Äñ - ‚Äñy‚Äñ < ‚Äñx - y‚Äñ :=
begin
  nth_rewrite 0 ‚Üêsub_add_cancel x y at ‚ä¢ h,
  exact sub_lt_iff_lt_add.2 (norm_add_lt_of_not_same_ray $ Œª H', h $ H'.add_left same_ray.rfl),
end

lemma abs_lt_norm_sub_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : |‚Äñx‚Äñ - ‚Äñy‚Äñ| < ‚Äñx - y‚Äñ :=
begin
  refine abs_sub_lt_iff.2 ‚ü®lt_norm_sub_of_not_same_ray h, _‚ü©,
  rw norm_sub_rev,
  exact lt_norm_sub_of_not_same_ray (mt same_ray.symm h),
end

/-- In a strictly convex space, two vectors `x`, `y` are in the same ray if and only if the triangle
inequality for `x` and `y` becomes an equality. -/
lemma same_ray_iff_norm_add : same_ray ‚Ñù x y ‚Üî ‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
‚ü®same_ray.norm_add, Œª h, not_not.1 $ Œª h', (norm_add_lt_of_not_same_ray h').ne h‚ü©

/-- If `x` and `y` are two vectors in a strictly convex space have the same norm and the norm of
their sum is equal to the sum of their norms, then they are equal. -/
lemma eq_of_norm_eq_of_norm_add_eq (h‚ÇÅ : ‚Äñx‚Äñ = ‚Äñy‚Äñ) (h‚ÇÇ : ‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ) : x = y :=
(same_ray_iff_norm_add.mpr h‚ÇÇ).eq_of_norm_eq h‚ÇÅ

/-- In a strictly convex space, two vectors `x`, `y` are not in the same ray if and only if the
triangle inequality for `x` and `y` is strict. -/
lemma not_same_ray_iff_norm_add_lt : ¬¨ same_ray ‚Ñù x y ‚Üî ‚Äñx + y‚Äñ < ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
same_ray_iff_norm_add.not.trans (norm_add_le _ _).lt_iff_ne.symm

lemma same_ray_iff_norm_sub : same_ray ‚Ñù x y ‚Üî ‚Äñx - y‚Äñ = |‚Äñx‚Äñ - ‚Äñy‚Äñ| :=
‚ü®same_ray.norm_sub, Œª h, not_not.1 $ Œª h', (abs_lt_norm_sub_of_not_same_ray h').ne' h‚ü©

lemma not_same_ray_iff_abs_lt_norm_sub : ¬¨ same_ray ‚Ñù x y ‚Üî |‚Äñx‚Äñ - ‚Äñy‚Äñ| < ‚Äñx - y‚Äñ :=
same_ray_iff_norm_sub.not.trans $ ne_comm.trans (abs_norm_sub_norm_le _ _).lt_iff_ne.symm

/-- In a strictly convex space, the triangle inequality turns into an equality if and only if the
middle point belongs to the segment joining two other points. -/
lemma dist_add_dist_eq_iff : dist x y + dist y z = dist x z ‚Üî y ‚àà [x -[‚Ñù] z] :=
by simp only [mem_segment_iff_same_ray, same_ray_iff_norm_add, dist_eq_norm',
  sub_add_sub_cancel', eq_comm]

lemma norm_midpoint_lt_iff (h : ‚Äñx‚Äñ = ‚Äñy‚Äñ) : ‚Äñ(1/2 : ‚Ñù) ‚Ä¢ (x + y)‚Äñ < ‚Äñx‚Äñ ‚Üî x ‚â† y :=
by rw [norm_smul, real.norm_of_nonneg (one_div_nonneg.2 zero_le_two), ‚Üêinv_eq_one_div,
    ‚Üêdiv_eq_inv_mul, div_lt_iff (zero_lt_two' ‚Ñù), mul_two, ‚Üênot_same_ray_iff_of_norm_eq h,
    not_same_ray_iff_norm_add_lt, h]

variables {F : Type*} [normed_add_comm_group F] [normed_space ‚Ñù F]
variables {PF : Type*} {PE : Type*} [metric_space PF] [metric_space PE]
variables [normed_add_torsor F PF] [normed_add_torsor E PE]

include E

lemma eq_line_map_of_dist_eq_mul_of_dist_eq_mul {x y z : PE} (hxy : dist x y = r * dist x z)
  (hyz : dist y z = (1 - r) * dist x z) :
  y = affine_map.line_map x z r :=
begin
  have : y -·µ• x ‚àà [(0 : E) -[‚Ñù] z -·µ• x],
  { rw [‚Üê dist_add_dist_eq_iff, dist_zero_left, dist_vsub_cancel_right, ‚Üê dist_eq_norm_vsub',
      ‚Üê dist_eq_norm_vsub', hxy, hyz, ‚Üê add_mul, add_sub_cancel'_right, one_mul] },
  rcases eq_or_ne x z with rfl|hne,
  { obtain rfl : y = x, by simpa,
    simp },
  { rw [‚Üê dist_ne_zero] at hne,
    rcases this with ‚ü®a, b, ha, hb, hab, H‚ü©,
    rw [smul_zero, zero_add] at H,
    have H' := congr_arg norm H,
    rw [norm_smul, real.norm_of_nonneg hb, ‚Üê dist_eq_norm_vsub', ‚Üê dist_eq_norm_vsub', hxy,
      mul_left_inj' hne] at H',
    rw [affine_map.line_map_apply, ‚Üê H', H, vsub_vadd] },
end

lemma eq_midpoint_of_dist_eq_half {x y z : PE} (hx : dist x y = dist x z / 2)
  (hy : dist y z = dist x z / 2) : y = midpoint ‚Ñù x z :=
begin
  apply eq_line_map_of_dist_eq_mul_of_dist_eq_mul,
  { rwa [inv_of_eq_inv, ‚Üê div_eq_inv_mul] },
  { rwa [inv_of_eq_inv, ‚Üê one_div, sub_half, one_div, ‚Üê div_eq_inv_mul] }
end

namespace isometry

include F

/-- An isometry of `normed_add_torsor`s for real normed spaces, strictly convex in the case of
the codomain, is an affine isometry.  Unlike Mazur-Ulam, this does not require the isometry to
be surjective.  -/
noncomputable def affine_isometry_of_strict_convex_space {f : PF ‚Üí PE} (hi : isometry f) :
  PF ‚Üí·µÉ‚Å±[‚Ñù] PE :=
{ norm_map := Œª x, by simp [affine_map.of_map_midpoint, ‚Üêdist_eq_norm_vsub E, hi.dist_eq],
  ..affine_map.of_map_midpoint f (Œª x y, begin
    apply eq_midpoint_of_dist_eq_half,
    { rw [hi.dist_eq, hi.dist_eq, dist_left_midpoint, real.norm_of_nonneg zero_le_two,
        div_eq_inv_mul] },
    { rw [hi.dist_eq, hi.dist_eq, dist_midpoint_right, real.norm_of_nonneg zero_le_two,
        div_eq_inv_mul] },
  end) hi.continuous }

@[simp] lemma coe_affine_isometry_of_strict_convex_space {f : PF ‚Üí PE} (hi : isometry f) :
  ‚áë(hi.affine_isometry_of_strict_convex_space) = f :=
rfl

@[simp] lemma affine_isometry_of_strict_convex_space_apply {f : PF ‚Üí PE} (hi : isometry f)
  (p : PF) :
  hi.affine_isometry_of_strict_convex_space p = f p :=
rfl

end isometry
