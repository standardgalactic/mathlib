/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.specific_limits.basic
import topology.urysohns_lemma
import topology.continuous_function.bounded
import topology.uniform_space.cauchy

/-!
# Metrizability of a T‚ÇÉ topological space with second countable topology

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define metrizable topological spaces, i.e., topological spaces for which there
exists a metric space structure that generates the same topology.

We also show that a T‚ÇÉ topological space with second countable topology `X` is metrizable.

First we prove that `X` can be embedded into `l^‚àû`, then use this embedding to pull back the metric
space structure.
-/

open set filter metric
open_locale bounded_continuous_function filter topology

namespace topological_space

variables {Œπ X Y : Type*} {œÄ : Œπ ‚Üí Type*} [topological_space X] [topological_space Y]
  [finite Œπ] [Œ† i, topological_space (œÄ i)]

/-- A topological space is *pseudo metrizable* if there exists a pseudo metric space structure
compatible with the topology. To endow such a space with a compatible distance, use
`letI : pseudo_metric_space X := topological_space.pseudo_metrizable_space_pseudo_metric X`. -/
class pseudo_metrizable_space (X : Type*) [t : topological_space X] : Prop :=
(exists_pseudo_metric : ‚àÉ (m : pseudo_metric_space X), m.to_uniform_space.to_topological_space = t)

@[priority 100]
instance _root_.pseudo_metric_space.to_pseudo_metrizable_space {X : Type*}
  [m : pseudo_metric_space X] :
  pseudo_metrizable_space X :=
‚ü®‚ü®m, rfl‚ü©‚ü©

/-- Construct on a metrizable space a metric compatible with the topology. -/
noncomputable def pseudo_metrizable_space_pseudo_metric
  (X : Type*) [topological_space X] [h : pseudo_metrizable_space X] :
  pseudo_metric_space X :=
h.exists_pseudo_metric.some.replace_topology h.exists_pseudo_metric.some_spec.symm

instance pseudo_metrizable_space_prod [pseudo_metrizable_space X] [pseudo_metrizable_space Y] :
  pseudo_metrizable_space (X √ó Y) :=
begin
  letI : pseudo_metric_space X := pseudo_metrizable_space_pseudo_metric X,
  letI : pseudo_metric_space Y := pseudo_metrizable_space_pseudo_metric Y,
  apply_instance
end

/-- Given an inducing map of a topological space into a pseudo metrizable space, the source space
is also pseudo metrizable. -/
lemma _root_.inducing.pseudo_metrizable_space [pseudo_metrizable_space Y] {f : X ‚Üí Y}
  (hf : inducing f) :
  pseudo_metrizable_space X :=
begin
  letI : pseudo_metric_space Y := pseudo_metrizable_space_pseudo_metric Y,
  exact ‚ü®‚ü®hf.comap_pseudo_metric_space, rfl‚ü©‚ü©
end

/-- Every pseudo-metrizable space is first countable. -/
@[priority 100]
instance pseudo_metrizable_space.first_countable_topology [h : pseudo_metrizable_space X] :
  topological_space.first_countable_topology X :=
begin
  unfreezingI { rcases h with ‚ü®_, hm‚ü©, rw ‚Üêhm },
  exact @uniform_space.first_countable_topology X pseudo_metric_space.to_uniform_space
    emetric.uniformity.filter.is_countably_generated,
end

instance pseudo_metrizable_space.subtype [pseudo_metrizable_space X]
  (s : set X) : pseudo_metrizable_space s :=
inducing_coe.pseudo_metrizable_space

instance pseudo_metrizable_space_pi [Œ† i, pseudo_metrizable_space (œÄ i)] :
  pseudo_metrizable_space (Œ† i, œÄ i) :=
by { casesI nonempty_fintype Œπ, letI := Œª i, pseudo_metrizable_space_pseudo_metric (œÄ i),
  apply_instance }

/-- A topological space is metrizable if there exists a metric space structure compatible with the
topology. To endow such a space with a compatible distance, use
`letI : metric_space X := topological_space.metrizable_space_metric X` -/
class metrizable_space (X : Type*) [t : topological_space X] : Prop :=
(exists_metric : ‚àÉ (m : metric_space X), m.to_uniform_space.to_topological_space = t)

@[priority 100]
instance _root_.metric_space.to_metrizable_space {X : Type*} [m : metric_space X] :
  metrizable_space X :=
‚ü®‚ü®m, rfl‚ü©‚ü©

@[priority 100]
instance metrizable_space.to_pseudo_metrizable_space [h : metrizable_space X] :
  pseudo_metrizable_space X :=
‚ü®let ‚ü®m, hm‚ü© := h.1 in ‚ü®m.to_pseudo_metric_space, hm‚ü©‚ü©

/-- Construct on a metrizable space a metric compatible with the topology. -/
noncomputable def metrizable_space_metric (X : Type*) [topological_space X]
  [h : metrizable_space X] :
  metric_space X :=
h.exists_metric.some.replace_topology h.exists_metric.some_spec.symm

@[priority 100]
instance t2_space_of_metrizable_space [metrizable_space X] : t2_space X :=
by { letI : metric_space X := metrizable_space_metric X, apply_instance }

instance metrizable_space_prod [metrizable_space X] [metrizable_space Y] :
  metrizable_space (X √ó Y) :=
begin
  letI : metric_space X := metrizable_space_metric X,
  letI : metric_space Y := metrizable_space_metric Y,
  apply_instance
end

/-- Given an embedding of a topological space into a metrizable space, the source space is also
metrizable. -/
lemma _root_.embedding.metrizable_space [metrizable_space Y] {f : X ‚Üí Y} (hf : embedding f) :
  metrizable_space X :=
begin
  letI : metric_space Y := metrizable_space_metric Y,
  exact ‚ü®‚ü®hf.comap_metric_space f, rfl‚ü©‚ü©
end

instance metrizable_space.subtype [metrizable_space X] (s : set X) : metrizable_space s :=
embedding_subtype_coe.metrizable_space

instance metrizable_space_pi [Œ† i, metrizable_space (œÄ i)] : metrizable_space (Œ† i, œÄ i) :=
by { casesI nonempty_fintype Œπ, letI := Œª i, metrizable_space_metric (œÄ i), apply_instance }

variables (X) [t3_space X] [second_countable_topology X]

/-- A T‚ÇÉ topological space with second countable topology can be embedded into `l^‚àû = ‚Ñï ‚Üí·µá ‚Ñù`.
-/
lemma exists_embedding_l_infty : ‚àÉ f : X ‚Üí (‚Ñï ‚Üí·µá ‚Ñù), embedding f :=
begin
  haveI : normal_space X := normal_space_of_t3_second_countable X,
  -- Choose a countable basis, and consider the set `s` of pairs of set `(U, V)` such that `U ‚àà B`,
  -- `V ‚àà B`, and `closure U ‚äÜ V`.
  rcases exists_countable_basis X with ‚ü®B, hBc, -, hB‚ü©,
  set s : set (set X √ó set X) := {UV ‚àà B √óÀ¢ B| closure UV.1 ‚äÜ UV.2},
  -- `s` is a countable set.
  haveI : encodable s := ((hBc.prod hBc).mono (inter_subset_left _ _)).to_encodable,
  -- We don't have the space of bounded (possibly discontinuous) functions, so we equip `s`
  -- with the discrete topology and deal with `s ‚Üí·µá ‚Ñù` instead.
  letI : topological_space s := ‚ä•, haveI : discrete_topology s := ‚ü®rfl‚ü©,
  rsuffices ‚ü®f, hf‚ü© : ‚àÉ f : X ‚Üí (s ‚Üí·µá ‚Ñù), embedding f,
  { exact ‚ü®Œª x, (f x).extend (encodable.encode' s) 0, (bounded_continuous_function.isometry_extend
      (encodable.encode' s) (0 : ‚Ñï ‚Üí·µá ‚Ñù)).embedding.comp hf‚ü© },
  have hd : ‚àÄ UV : s, disjoint (closure UV.1.1) (UV.1.2·∂ú) :=
    Œª UV, disjoint_compl_right.mono_right (compl_subset_compl.2 UV.2.2),
  -- Choose a sequence of `Œµ‚Çô > 0`, `n : s`, that is bounded above by `1` and tends to zero
  -- along the `cofinite` filter.
  obtain ‚ü®Œµ, Œµ01, hŒµ‚ü© : ‚àÉ Œµ : s ‚Üí ‚Ñù, (‚àÄ UV, Œµ UV ‚àà Ioc (0 : ‚Ñù) 1) ‚àß tendsto Œµ cofinite (ùìù 0),
  { rcases pos_sum_of_encodable zero_lt_one s with ‚ü®Œµ, Œµ0, c, hŒµc, hc1‚ü©,
    refine ‚ü®Œµ, Œª UV, ‚ü®Œµ0 UV, _‚ü©, hŒµc.summable.tendsto_cofinite_zero‚ü©,
    exact (le_has_sum hŒµc UV $ Œª _ _, (Œµ0 _).le).trans hc1 },
  /- For each `UV = (U, V) ‚àà s` we use Urysohn's lemma to choose a function `f UV` that is equal to
  zero on `U` and is equal to `Œµ UV` on the complement to `V`. -/
  have : ‚àÄ UV : s, ‚àÉ f : C(X, ‚Ñù), eq_on f 0 UV.1.1 ‚àß eq_on f (Œª _, Œµ UV) UV.1.2·∂ú ‚àß
    ‚àÄ x, f x ‚àà Icc 0 (Œµ UV),
  { intro UV,
    rcases exists_continuous_zero_one_of_closed is_closed_closure
      (hB.is_open UV.2.1.2).is_closed_compl (hd UV) with ‚ü®f, hf‚ÇÄ, hf‚ÇÅ, hf01‚ü©,
    exact ‚ü®Œµ UV ‚Ä¢ f, Œª x hx, by simp [hf‚ÇÄ (subset_closure hx)], Œª x hx, by simp [hf‚ÇÅ hx],
      Œª x, ‚ü®mul_nonneg (Œµ01 _).1.le (hf01 _).1, mul_le_of_le_one_right (Œµ01 _).1.le (hf01 _).2‚ü©‚ü© },
  choose f hf0 hfŒµ hf0Œµ,
  have hf01 : ‚àÄ UV x, f UV x ‚àà Icc (0 : ‚Ñù) 1,
    from Œª UV x, Icc_subset_Icc_right (Œµ01 _).2 (hf0Œµ _ _),
  /- The embedding is given by `F x UV = f UV x`. -/
  set F : X ‚Üí s ‚Üí·µá ‚Ñù := Œª x, ‚ü®‚ü®Œª UV, f UV x, continuous_of_discrete_topology‚ü©, 1, Œª UV‚ÇÅ UV‚ÇÇ,
    real.dist_le_of_mem_Icc_01 (hf01 _ _) (hf01 _ _)‚ü©,
  have hF : ‚àÄ x UV, F x UV = f UV x := Œª _ _, rfl,
  refine ‚ü®F, embedding.mk' _ (Œª x y hxy, _) (Œª x, le_antisymm _ _)‚ü©,
  { /- First we prove that `F` is injective. Indeed, if `F x = F y` and `x ‚â† y`, then we can find
    `(U, V) ‚àà s` such that `x ‚àà U` and `y ‚àâ V`, hence `F x UV = 0 ‚â† Œµ UV = F y UV`. -/
    refine not_not.1 (Œª Hne, _), -- `by_contra Hne` timeouts
    rcases hB.mem_nhds_iff.1 (is_open_ne.mem_nhds Hne) with ‚ü®V, hVB, hxV, hVy‚ü©,
    rcases hB.exists_closure_subset (hB.mem_nhds hVB hxV) with ‚ü®U, hUB, hxU, hUV‚ü©,
    set UV : ‚Ü•s := ‚ü®(U, V), ‚ü®hUB, hVB‚ü©, hUV‚ü©,
    apply (Œµ01 UV).1.ne,
    calc (0 : ‚Ñù) = F x UV : (hf0 UV hxU).symm
             ... = F y UV : by rw hxy
             ... = Œµ UV   : hfŒµ UV (Œª h : y ‚àà V, hVy h rfl) },
  { /- Now we prove that each neighborhood `V` of `x : X` include a preimage of a neighborhood of
    `F x` under `F`. Without loss of generality, `V` belongs to `B`. Choose `U ‚àà B` such that
    `x ‚àà V` and `closure V ‚äÜ U`. Then the preimage of the `(Œµ (U, V))`-neighborhood of `F x`
    is included by `V`. -/
    refine ((nhds_basis_ball.comap _).le_basis_iff hB.nhds_has_basis).2 _,
    rintro V ‚ü®hVB, hxV‚ü©,
    rcases hB.exists_closure_subset (hB.mem_nhds hVB hxV) with ‚ü®U, hUB, hxU, hUV‚ü©,
    set UV : ‚Ü•s := ‚ü®(U, V), ‚ü®hUB, hVB‚ü©, hUV‚ü©,
    refine ‚ü®Œµ UV, (Œµ01 UV).1, Œª y (hy : dist (F y) (F x) < Œµ UV), _‚ü©,
    replace hy : dist (F y UV) (F x UV) < Œµ UV,
      from (bounded_continuous_function.dist_coe_le_dist _).trans_lt hy,
    contrapose! hy,
    rw [hF, hF, hfŒµ UV hy, hf0 UV hxU, pi.zero_apply, dist_zero_right],
    exact le_abs_self _ },
  { /- Finally, we prove that `F` is continuous. Given `Œ¥ > 0`, consider the set `T` of `(U, V) ‚àà s`
    such that `Œµ (U, V) ‚â• Œ¥`. Since `Œµ` tends to zero, `T` is finite. Since each `f` is continuous,
    we can choose a neighborhood such that `dist (F y (U, V)) (F x (U, V)) ‚â§ Œ¥` for any
    `(U, V) ‚àà T`. For `(U, V) ‚àâ T`, the same inequality is true because both `F y (U, V)` and
    `F x (U, V)` belong to the interval `[0, Œµ (U, V)]`. -/
    refine (nhds_basis_closed_ball.comap _).ge_iff.2 (Œª Œ¥ Œ¥0, _),
    have h_fin : {UV : s | Œ¥ ‚â§ Œµ UV}.finite, by simpa only [‚Üê not_lt] using hŒµ (gt_mem_nhds Œ¥0),
    have : ‚àÄ·∂† y in ùìù x, ‚àÄ UV, Œ¥ ‚â§ Œµ UV ‚Üí dist (F y UV) (F x UV) ‚â§ Œ¥,
    { refine (eventually_all_finite h_fin).2 (Œª UV hUV, _),
      exact (f UV).continuous.tendsto x (closed_ball_mem_nhds _ Œ¥0) },
    refine this.mono (Œª y hy, (bounded_continuous_function.dist_le Œ¥0.le).2 $ Œª UV, _),
    cases le_total Œ¥ (Œµ UV) with hle hle,
    exacts [hy _ hle, (real.dist_le_of_mem_Icc (hf0Œµ _ _) (hf0Œµ _ _)).trans (by rwa sub_zero)] }
end

/-- *Urysohn's metrization theorem* (Tychonoff's version): a T‚ÇÉ topological space with second
countable topology `X` is metrizable, i.e., there exists a metric space structure that generates the
same topology. -/
lemma metrizable_space_of_t3_second_countable : metrizable_space X :=
let ‚ü®f, hf‚ü© := exists_embedding_l_infty X in hf.metrizable_space

instance : metrizable_space ennreal := metrizable_space_of_t3_second_countable ennreal

end topological_space
