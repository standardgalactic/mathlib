/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import number_theory.number_field.embeddings

/-!
# Canonical embedding of a number field

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` is the ring homomorphism
`K ‚Üí+* ‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that sends `x ‚àà K` to `(œÜ_‚ÇÅ(x),...,œÜ_r‚ÇÅ(x)) √ó (œà_‚ÇÅ(x),..., œà_r‚ÇÇ(x))` where
`œÜ_‚ÇÅ,...,œÜ_r‚ÇÅ` are its real embeddings and `œà_‚ÇÅ,..., œà_r‚ÇÇ` are its complex embeddings (up to
complex conjugation).

## Main definitions and results

* `number_field.canonical_embedding.ring_of_integers.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

## Tags

number field, infinite places
-/

noncomputable theory

open function finite_dimensional finset fintype number_field number_field.infinite_place metric
module
open_locale classical number_field

variables (K : Type*) [field K]

namespace number_field.canonical_embedding

-- The ambient space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` with `(r‚ÇÅ, r‚ÇÇ)` the signature of `K`.
localized "notation `E` :=
  ({w : infinite_place K // is_real w} ‚Üí ‚Ñù) √ó ({w : infinite_place K // is_complex w} ‚Üí ‚ÑÇ)"
  in canonical_embedding

lemma space_rank [number_field K] :
  finrank ‚Ñù E = finrank ‚Ñö K :=
begin
  haveI : module.free ‚Ñù ‚ÑÇ := infer_instance,
  rw [finrank_prod, finrank_pi, finrank_pi_fintype, complex.finrank_real_complex,
    finset.sum_const, finset.card_univ, ‚Üê card_real_embeddings, algebra.id.smul_eq_mul, mul_comm,
    ‚Üê card_complex_embeddings, ‚Üê number_field.embeddings.card K ‚ÑÇ, fintype.card_subtype_compl,
    nat.add_sub_of_le (fintype.card_subtype_le _)],
end

lemma non_trivial_space [number_field K] : nontrivial E :=
begin
  obtain ‚ü®w‚ü© := infinite_place.nonempty K,
  obtain hw | hw := w.is_real_or_is_complex,
  { haveI : nonempty {w : infinite_place K // is_real w} := ‚ü®‚ü®w, hw‚ü©‚ü©,
    exact nontrivial_prod_left },
  { haveI : nonempty {w : infinite_place K // is_complex w} := ‚ü®‚ü®w, hw‚ü©‚ü©,
    exact nontrivial_prod_right }
end

/-- The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` into `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def _root_.number_field.canonical_embedding : K ‚Üí+* E :=
ring_hom.prod (pi.ring_hom (Œª w, w.prop.embedding)) (pi.ring_hom (Œª w, w.val.embedding))

lemma _root_.number_field.canonical_embedding_injective [number_field K] :
  injective (number_field.canonical_embedding K) :=
  @ring_hom.injective _ _ _ _ (non_trivial_space K) _

open number_field

variable {K}

@[simp]
lemma apply_at_real_infinite_place (w : {w : infinite_place K // is_real w}) (x : K) :
  (number_field.canonical_embedding K x).1 w = w.prop.embedding x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

@[simp]
lemma apply_at_complex_infinite_place (w : { w : infinite_place K // is_complex w}) (x : K) :
  (number_field.canonical_embedding K x).2 w = embedding w.val x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

lemma nnnorm_eq [number_field K] (x : K) :
  ‚Äñcanonical_embedding K x‚Äñ‚Çä = finset.univ.sup (Œª w : infinite_place K, ‚ü®w x, map_nonneg w x‚ü©) :=
begin
  rw [prod.nnnorm_def', pi.nnnorm_def, pi.nnnorm_def],
  rw ( _ : finset.univ = {w : infinite_place K | is_real w}.to_finset
    ‚à™ {w : infinite_place K | is_complex w}.to_finset),
  { rw [finset.sup_union, sup_eq_max],
    refine congr_arg2 _ _ _,
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K, is_real w))
        (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_real_infinite_place, coe_nnnorm, real.norm_eq_abs,
        function.embedding.coe_subtype, subtype.coe_mk, is_real.abs_embedding_apply], },
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K,
        is_complex w)) (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_complex_infinite_place, subtype.val_eq_coe, coe_nnnorm,
        complex.norm_eq_abs, function.embedding.coe_subtype, subtype.coe_mk, abs_embedding], }},
  { ext w,
    simp only [w.is_real_or_is_complex, set.mem_set_of_eq, finset.mem_union, set.mem_to_finset,
      finset.mem_univ], },
end

lemma norm_le_iff [number_field K] (x : K) (r : ‚Ñù) :
  ‚Äñcanonical_embedding K x‚Äñ ‚â§ r ‚Üî ‚àÄ w : infinite_place K, w x ‚â§ r :=
begin
  obtain hr | hr := lt_or_le r 0,
  { obtain ‚ü®w‚ü© := infinite_place.nonempty K,
    exact iff_of_false (hr.trans_le $ norm_nonneg _).not_le
      (Œª h, hr.not_le $ (map_nonneg w _).trans $ h _) },
  { lift r to nnreal using hr,
    simp_rw [‚Üê coe_nnnorm, nnnorm_eq, nnreal.coe_le_coe, finset.sup_le_iff, finset.mem_univ,
      forall_true_left, ‚Üênnreal.coe_le_coe, subtype.coe_mk] }
end

variables (K)

/-- The image of `ùìû K` as a subring of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def integer_lattice : subring E :=
(ring_hom.range (algebra_map (ùìû K) K)).map (canonical_embedding K)

/-- The linear equiv between `ùìû K` and the integer lattice. -/
def equiv_integer_lattice [number_field K] :
  ùìû K ‚âÉ‚Çó[‚Ñ§] integer_lattice K :=
linear_equiv.of_bijective
  { to_fun := Œª x, ‚ü®canonical_embedding K (algebra_map (ùìû K) K x), algebra_map (ùìû K) K x,
      by simp only [subring.mem_carrier, ring_hom.mem_range, exists_apply_eq_apply], rfl‚ü©,
    map_add' := Œª x y, by simpa only [map_add],
    map_smul' := Œª c x, by simpa only [zsmul_eq_mul, map_mul, map_int_cast] }
  begin
    refine ‚ü®Œª _ _ h, _,  Œª ‚ü®_, _, ‚ü®a, rfl‚ü©, rfl‚ü©, ‚ü®a, rfl‚ü©‚ü©,
    rw [linear_map.coe_mk, subtype.mk_eq_mk] at h,
    exact is_fraction_ring.injective (ùìû K) K (canonical_embedding_injective K h),
  end

lemma integer_lattice.inter_ball_finite [number_field K] (r : ‚Ñù) :
  ((integer_lattice K : set E) ‚à© (closed_ball 0 r)).finite :=
begin
  obtain hr | hr := lt_or_le r 0,
  {  simp [closed_ball_eq_empty.2 hr] },
  have heq :
    ‚àÄ x, canonical_embedding K x ‚àà closed_ball (0 : E) r ‚Üî ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ ‚â§ r,
  { simp only [‚Üê place_apply, ‚Üê infinite_place.coe_mk, mem_closed_ball_zero_iff, norm_le_iff],
    exact Œª x, le_iff_le x r, },
  convert (embeddings.finite_of_norm_le K ‚ÑÇ r).image (canonical_embedding K),
  ext, split,
  { rintro ‚ü®‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, hx2‚ü©,
    exact ‚ü®x, ‚ü®set_like.coe_mem x, (heq x).mp hx2‚ü©, rfl‚ü©, },
  { rintro ‚ü®x, ‚ü®hx1, hx2‚ü©, rfl‚ü©,
    exact ‚ü®‚ü®x, ‚ü®‚ü®x, hx1‚ü©, rfl‚ü©, rfl‚ü©, (heq x).mpr hx2‚ü©, }
end

instance [number_field K] : countable (integer_lattice K) :=
begin
  have : (‚ãÉ n : ‚Ñï, ((integer_lattice K : set E) ‚à© (closed_ball 0 n))).countable,
  { exact set.countable_Union (Œª n, (integer_lattice.inter_ball_finite K n).countable) },
  refine (this.mono _).to_subtype,
  rintro _ ‚ü®x, hx, rfl‚ü©,
  rw set.mem_Union,
  exact ‚ü®‚åà‚Äñcanonical_embedding K x‚Äñ‚åâ‚Çä, ‚ü®x, hx, rfl‚ü©, mem_closed_ball_zero_iff.2 (nat.le_ceil _)‚ü©,
end

end number_field.canonical_embedding
