/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Mario Carneiro, Reid Barton, Andrew Yang
-/
import category_theory.limits.kan_extension
import topology.category.Top.opens
import category_theory.adjunction.opposites

/-!
# Presheaves on a topological space

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define `presheaf C X` simply as `(opens X)·µí·µñ ‚•§ C`,
and inherit the category structure with natural transformations as morphisms.

We define
* `pushforward_obj {X Y : Top.{w}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) : Y.presheaf C`
with notation `f _* ‚Ñ±`
and for `‚Ñ± : X.presheaf C` provide the natural isomorphisms
* `pushforward.id : (ùüô X) _* ‚Ñ± ‚âÖ ‚Ñ±`
* `pushforward.comp : (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±)`
along with their `@[simp]` lemmas.

We also define the functors `pushforward` and `pullback` between the categories
`X.presheaf C` and `Y.presheaf C`, and provide their adjunction at
`pushforward_pullback_adjunction`.
-/

universes w v u

open category_theory
open topological_space
open opposite

variables (C : Type u) [category.{v} C]

namespace Top

/-- The category of `C`-valued presheaves on a (bundled) topological space `X`. -/
@[derive category, nolint has_nonempty_instance]
def presheaf (X : Top.{w}) : Type (max u v w) := (opens X)·µí·µñ ‚•§ C

variables {C}

namespace presheaf

local attribute [instance] concrete_category.has_coe_to_sort concrete_category.has_coe_to_fun

/-- Tag lemmas to use in `Top.presheaf.restrict_tac`.  -/
@[user_attribute]
meta def restrict_attr : user_attribute (tactic unit ‚Üí tactic unit) unit :=
{ name      := `sheaf_restrict,
  descr     := "tag lemmas to use in `Top.presheaf.restrict_tac`",
  cache_cfg :=
  { mk_cache := Œª ns, pure $ Œª t, do
    { ctx <- tactic.local_context,
      ctx.any_of (tactic.focus1 ‚àò (tactic.apply' >=> (Œª _, tactic.done)) >=> (Œª _, t)) <|>
      ns.any_of (tactic.focus1 ‚àò (tactic.resolve_name >=> tactic.to_expr >=> tactic.apply' >=>
        (Œª _, tactic.done)) >=> (Œª _, t)) },
    dependencies := [] } }

/-- A tactic to discharge goals of type `U ‚â§ V` for `Top.presheaf.restrict_open` -/
meta def restrict_tac : Œ† (n : ‚Ñï), tactic unit
| 0 := tactic.fail "`restrict_tac` failed"
| (n + 1) := monad.join (restrict_attr.get_cache <*> pure tactic.done) <|>
    `[apply' le_trans, mjoin (restrict_attr.get_cache <*> pure (restrict_tac n))]

/-- A tactic to discharge goals of type `U ‚â§ V` for `Top.presheaf.restrict_open`.
Defaults to three iterations. -/
meta def restrict_tac' := restrict_tac 3

attribute [sheaf_restrict] bot_le le_top le_refl inf_le_left inf_le_right le_sup_left le_sup_right

example {X : Top} {v w x y z : opens X} (h‚ÇÄ : v ‚â§ x) (h‚ÇÅ : x ‚â§ z ‚äì w) (h‚ÇÇ : x ‚â§ y ‚äì z) :
  v ‚â§ y := by restrict_tac'

/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_‚Çï i` (`h` stands for `hom`) for `i : U ‚ü∂ V`,
and the notation `x |_‚Çó U ‚ü™i‚ü´` (`l` stands for `le`) for `i : U ‚â§ V`.
-/
def restrict {X : Top} {C : Type*} [category C] [concrete_category C]
  {F : X.presheaf C} {V : opens X} (x : F.obj (op V)) {U : opens X} (h : U ‚ü∂ V) : F.obj (op U) :=
F.map h.op x

localized "infixl ` |_‚Çï `: 80 := Top.presheaf.restrict" in algebraic_geometry

localized "notation x ` |_‚Çó `: 80 U ` ‚ü™` e `‚ü´ ` :=
@Top.presheaf.restrict _ _ _ _ _ _ x U (@hom_of_le (opens _) _ U _ e)" in algebraic_geometry

/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_ U`, where the proof `U ‚â§ V` is inferred by
the tactic `Top.presheaf.restrict_tac'` -/
abbreviation restrict_open {X : Top} {C : Type*} [category C] [concrete_category C]
  {F : X.presheaf C} {V : opens X} (x : F.obj (op V)) (U : opens X)
  (e : U ‚â§ V . Top.presheaf.restrict_tac') : F.obj (op U) :=
x |_‚Çó U ‚ü™e‚ü´

localized "infixl ` |_ `: 80 := Top.presheaf.restrict_open" in algebraic_geometry

@[simp]
lemma restrict_restrict {X : Top} {C : Type*} [category C] [concrete_category C]
  {F : X.presheaf C} {U V W : opens X} (e‚ÇÅ : U ‚â§ V) (e‚ÇÇ : V ‚â§ W) (x : F.obj (op W)) :
    x |_ V |_ U = x |_ U :=
by { delta restrict_open restrict, rw [‚Üê comp_apply, ‚Üê functor.map_comp], refl }

@[simp]
lemma map_restrict {X : Top} {C : Type*} [category C] [concrete_category C]
  {F G : X.presheaf C} (e : F ‚ü∂ G) {U V : opens X} (h : U ‚â§ V) (x : F.obj (op V)) :
    e.app _ (x |_ U) = (e.app _ x) |_ U :=
by { delta restrict_open restrict, rw [‚Üê comp_apply, nat_trans.naturality, comp_apply] }

/-- Pushforward a presheaf on `X` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf
on `Y`. -/
def pushforward_obj {X Y : Top.{w}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) : Y.presheaf C :=
(opens.map f).op ‚ãô ‚Ñ±

infix ` _* `: 80 := pushforward_obj

@[simp] lemma pushforward_obj_obj {X Y : Top.{w}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (U : (opens Y)·µí·µñ) :
  (f _* ‚Ñ±).obj U = ‚Ñ±.obj ((opens.map f).op.obj U) := rfl

@[simp] lemma pushforward_obj_map {X Y : Top.{w}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C)
  {U V : (opens Y)·µí·µñ} (i : U ‚ü∂ V) :
  (f _* ‚Ñ±).map i = ‚Ñ±.map ((opens.map f).op.map i) := rfl

/--
An equality of continuous maps induces a natural isomorphism between the pushforwards of a presheaf
along those maps.
-/
def pushforward_eq {X Y : Top.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) :
  f _* ‚Ñ± ‚âÖ g _* ‚Ñ± :=
iso_whisker_right (nat_iso.op (opens.map_iso f g h).symm) ‚Ñ±

lemma pushforward_eq' {X Y : Top.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) :
  f _* ‚Ñ± = g _* ‚Ñ± :=
by rw h

@[simp] lemma pushforward_eq_hom_app
  {X Y : Top.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) (U) :
  (pushforward_eq h ‚Ñ±).hom.app U =
    ‚Ñ±.map (begin dsimp [functor.op], apply quiver.hom.op, apply eq_to_hom, rw h, end) :=
by simp [pushforward_eq]

lemma pushforward_eq'_hom_app
  {X Y : Top.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) (U) :
  nat_trans.app (eq_to_hom (pushforward_eq' h ‚Ñ±)) U = ‚Ñ±.map (eq_to_hom (by rw h)) :=
by simpa [eq_to_hom_map]

@[simp]
lemma pushforward_eq_rfl {X Y : Top.{w}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (U) :
  (pushforward_eq (rfl : f = f) ‚Ñ±).hom.app (op U) = ùüô _ :=
begin
  dsimp [pushforward_eq],
  simp,
end

lemma pushforward_eq_eq {X Y : Top.{w}} {f g : X ‚ü∂ Y} (h‚ÇÅ h‚ÇÇ : f = g) (‚Ñ± : X.presheaf C) :
  ‚Ñ±.pushforward_eq h‚ÇÅ = ‚Ñ±.pushforward_eq h‚ÇÇ :=
rfl

namespace pushforward
variables {X : Top.{w}} (‚Ñ± : X.presheaf C)

/-- The natural isomorphism between the pushforward of a presheaf along the identity continuous map
and the original presheaf. -/
def id : (ùüô X) _* ‚Ñ± ‚âÖ ‚Ñ± :=
(iso_whisker_right (nat_iso.op (opens.map_id X).symm) ‚Ñ±) ‚â™‚â´ functor.left_unitor _

lemma id_eq : (ùüô X) _* ‚Ñ± = ‚Ñ± :=
by { unfold pushforward_obj, rw opens.map_id_eq, erw functor.id_comp }

@[simp] lemma id_hom_app' (U) (p) :
  (id ‚Ñ±).hom.app (op ‚ü®U, p‚ü©) = ‚Ñ±.map (ùüô (op ‚ü®U, p‚ü©)) :=
by { dsimp [id], simp, }

local attribute [tidy] tactic.op_induction'

@[simp, priority 990] lemma id_hom_app (U) :
  (id ‚Ñ±).hom.app U = ‚Ñ±.map (eq_to_hom (opens.op_map_id_obj U)) :=
begin
  -- was `tidy`
  induction U using opposite.rec,
  cases U,
  rw [id_hom_app'],
  congr
end

@[simp] lemma id_inv_app' (U) (p) : (id ‚Ñ±).inv.app (op ‚ü®U, p‚ü©) = ‚Ñ±.map (ùüô (op ‚ü®U, p‚ü©)) :=
by { dsimp [id], simp, }

/-- The natural isomorphism between
the pushforward of a presheaf along the composition of two continuous maps and
the corresponding pushforward of a pushforward. -/
def comp {Y Z : Top.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±) :=
iso_whisker_right (nat_iso.op (opens.map_comp f g).symm) ‚Ñ±

lemma comp_eq {Y Z : Top.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g) _* ‚Ñ± = g _* (f _* ‚Ñ±) :=
rfl

@[simp] lemma comp_hom_app {Y Z : Top.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
  (comp ‚Ñ± f g).hom.app U = ùüô _ :=
by { dsimp [comp], tidy, }

@[simp] lemma comp_inv_app {Y Z : Top.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
  (comp ‚Ñ± f g).inv.app U = ùüô _ :=
by { dsimp [comp], tidy, }

end pushforward

/--
A morphism of presheaves gives rise to a morphisms of the pushforwards of those presheaves.
-/
@[simps]
def pushforward_map {X Y : Top.{w}} (f : X ‚ü∂ Y) {‚Ñ± ùí¢ : X.presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢) :
  f _* ‚Ñ± ‚ü∂ f _* ùí¢ :=
{ app := Œª U, Œ±.app _,
  naturality' := Œª U V i, by { erw Œ±.naturality, refl, } }

open category_theory.limits
section pullback
variable [has_colimits C]
noncomputable theory

/--
Pullback a presheaf on `Y` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf on `X`.

This is defined in terms of left Kan extensions, which is just a fancy way of saying
"take the colimits over the open sets whose preimage contains U".
-/
@[simps]
def pullback_obj {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : Y.presheaf C) : X.presheaf C :=
(Lan (opens.map f).op).obj ‚Ñ±

/-- Pulling back along continuous maps is functorial. -/
def pullback_map {X Y : Top.{v}} (f : X ‚ü∂ Y) {‚Ñ± ùí¢ : Y.presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢) :
  pullback_obj f ‚Ñ± ‚ü∂ pullback_obj f ùí¢ :=
(Lan (opens.map f).op).map Œ±

/-- If `f '' U` is open, then `f‚Åª¬π‚Ñ± U ‚âÖ ‚Ñ± (f '' U)`.  -/
@[simps]
def pullback_obj_obj_of_image_open {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : Y.presheaf C) (U : opens X)
  (H : is_open (f '' U)) : (pullback_obj f ‚Ñ±).obj (op U) ‚âÖ ‚Ñ±.obj (op ‚ü®_, H‚ü©) :=
begin
  let x : costructured_arrow (opens.map f).op (op U) := begin
    refine @costructured_arrow.mk _ _ _ _ _ (op (opens.mk (f '' U) H)) _ _,
    exact ((@hom_of_le _ _ _ ((opens.map f).obj ‚ü®_, H‚ü©) (set.image_preimage.le_u_l _)).op),
  end,
  have hx : is_terminal x :=
  { lift := Œª s,
    begin
      fapply costructured_arrow.hom_mk,
      change op (unop _) ‚ü∂ op (‚ü®_, H‚ü© : opens _),
      refine (hom_of_le _).op,
      exact (set.image_subset f s.X.hom.unop.le).trans (set.image_preimage.l_u_le ‚Üë(unop s.X.left)),
      simp
    end },
  exact is_colimit.cocone_point_unique_up_to_iso
    (colimit.is_colimit _)
    (colimit_of_diagram_terminal hx _),
end

namespace pullback
variables {X Y : Top.{v}} (‚Ñ± : Y.presheaf C)

/-- The pullback along the identity is isomorphic to the original presheaf. -/
def id : pullback_obj (ùüô _) ‚Ñ± ‚âÖ ‚Ñ± :=
nat_iso.of_components
  (Œª U, pullback_obj_obj_of_image_open (ùüô _) ‚Ñ± (unop U) (by simpa using U.unop.2) ‚â™‚â´
    ‚Ñ±.map_iso (eq_to_iso (by simp)))
  (Œª U V i,
  begin
      ext, simp,
      erw colimit.pre_desc_assoc,
      erw colimit.Œπ_desc_assoc,
      erw colimit.Œπ_desc_assoc,
      dsimp, simp only [‚Üê‚Ñ±.map_comp], congr
  end)

lemma id_inv_app (U : opens Y) :
  (id ‚Ñ±).inv.app (op U) = colimit.Œπ (Lan.diagram (opens.map (ùüô Y)).op ‚Ñ± (op U))
    (@costructured_arrow.mk _ _ _ _ _ (op U) _ (eq_to_hom (by simp))) :=
begin
  rw [‚Üê category.id_comp ((id ‚Ñ±).inv.app (op U)), ‚Üê nat_iso.app_inv, iso.comp_inv_eq],
  dsimp [id],
  rw colimit.Œπ_desc_assoc,
  dsimp,
  rw [‚Üê ‚Ñ±.map_comp, ‚Üê ‚Ñ±.map_id], refl,
end

end pullback
end pullback
variable (C)

/--
The pushforward functor.
-/
def pushforward {X Y : Top.{w}} (f : X ‚ü∂ Y) : X.presheaf C ‚•§ Y.presheaf C :=
{ obj := pushforward_obj f,
  map := @pushforward_map _ _ X Y f }

@[simp]
lemma pushforward_map_app' {X Y : Top.{w}} (f : X ‚ü∂ Y)
  {‚Ñ± ùí¢ : X.presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢) {U : (opens Y)·µí·µñ} :
  ((pushforward C f).map Œ±).app U = Œ±.app (op $ (opens.map f).obj U.unop) := rfl

lemma id_pushforward {X : Top.{w}} : pushforward C (ùüô X) = ùü≠ (X.presheaf C) :=
begin
  apply category_theory.functor.ext,
  { intros,
    ext U,
    have h := f.congr, erw h (opens.op_map_id_obj U),
    simpa [eq_to_hom_map], },
  { intros, apply pushforward.id_eq },
end

section iso

/-- A homeomorphism of spaces gives an equivalence of categories of presheaves. -/
@[simps] def presheaf_equiv_of_iso {X Y : Top} (H : X ‚âÖ Y) :
  X.presheaf C ‚âå Y.presheaf C :=
equivalence.congr_left (opens.map_map_iso H).symm.op

variable {C}

/--
If `H : X ‚âÖ Y` is a homeomorphism,
then given an `H _* ‚Ñ± ‚ü∂ ùí¢`, we may obtain an `‚Ñ± ‚ü∂ H ‚Åª¬π _* ùí¢`.
-/
def to_pushforward_of_iso {X Y : Top} (H : X ‚âÖ Y) {‚Ñ± : X.presheaf C} {ùí¢ : Y.presheaf C}
  (Œ± : H.hom _* ‚Ñ± ‚ü∂ ùí¢) : ‚Ñ± ‚ü∂ H.inv _* ùí¢ :=
(presheaf_equiv_of_iso _ H).to_adjunction.hom_equiv ‚Ñ± ùí¢ Œ±

@[simp]
lemma to_pushforward_of_iso_app {X Y : Top} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : X.presheaf C} {ùí¢ : Y.presheaf C}
  (H‚ÇÇ : H‚ÇÅ.hom _* ‚Ñ± ‚ü∂ ùí¢) (U : (opens X)·µí·µñ) :
(to_pushforward_of_iso H‚ÇÅ H‚ÇÇ).app U =
  ‚Ñ±.map (eq_to_hom (by simp [opens.map, set.preimage_preimage])) ‚â´
  H‚ÇÇ.app (op ((opens.map H‚ÇÅ.inv).obj (unop U))) :=
begin
  delta to_pushforward_of_iso,
  simp only [equiv.to_fun_as_coe, nat_trans.comp_app, equivalence.equivalence_mk'_unit,
    eq_to_hom_map, eq_to_hom_op, eq_to_hom_trans, presheaf_equiv_of_iso_unit_iso_hom_app_app,
    equivalence.to_adjunction, equivalence.equivalence_mk'_counit,
    presheaf_equiv_of_iso_inverse_map_app, adjunction.mk_of_unit_counit_hom_equiv_apply],
  congr,
end

/--
If `H : X ‚âÖ Y` is a homeomorphism,
then given an `H _* ‚Ñ± ‚ü∂ ùí¢`, we may obtain an `‚Ñ± ‚ü∂ H ‚Åª¬π _* ùí¢`.
-/
def pushforward_to_of_iso {X Y : Top} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : Y.presheaf C} {ùí¢ : X.presheaf C}
  (H‚ÇÇ : ‚Ñ± ‚ü∂ H‚ÇÅ.hom _* ùí¢) : H‚ÇÅ.inv _* ‚Ñ± ‚ü∂ ùí¢ :=
((presheaf_equiv_of_iso _ H‚ÇÅ.symm).to_adjunction.hom_equiv ‚Ñ± ùí¢).symm H‚ÇÇ

@[simp]
lemma pushforward_to_of_iso_app {X Y : Top} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : Y.presheaf C} {ùí¢ : X.presheaf C}
  (H‚ÇÇ : ‚Ñ± ‚ü∂ H‚ÇÅ.hom _* ùí¢) (U : (opens X)·µí·µñ) :
(pushforward_to_of_iso H‚ÇÅ H‚ÇÇ).app U =
  H‚ÇÇ.app (op ((opens.map H‚ÇÅ.inv).obj (unop U))) ‚â´
  ùí¢.map (eq_to_hom (by simp [opens.map, set.preimage_preimage])) :=
by simpa [pushforward_to_of_iso, equivalence.to_adjunction]

end iso

variables (C) [has_colimits C]

/-- Pullback a presheaf on `Y` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf
on `X`. -/
@[simps map_app]
def pullback {X Y : Top.{v}} (f : X ‚ü∂ Y) : Y.presheaf C ‚•§ X.presheaf C := Lan (opens.map f).op

@[simp] lemma pullback_obj_eq_pullback_obj {C} [category C] [has_colimits C] {X Y : Top.{w}}
  (f : X ‚ü∂ Y) (‚Ñ± : Y.presheaf C) : (pullback C f).obj ‚Ñ± = pullback_obj f ‚Ñ± := rfl

/-- The pullback and pushforward along a continuous map are adjoint to each other. -/
@[simps unit_app_app counit_app_app]
def pushforward_pullback_adjunction {X Y : Top.{v}} (f : X ‚ü∂ Y) :
  pullback C f ‚ä£ pushforward C f := Lan.adjunction _ _

/-- Pulling back along a homeomorphism is the same as pushing forward along its inverse. -/
def pullback_hom_iso_pushforward_inv {X Y : Top.{v}} (H : X ‚âÖ Y) :
  pullback C H.hom ‚âÖ pushforward C H.inv :=
adjunction.left_adjoint_uniq
  (pushforward_pullback_adjunction C H.hom)
  (presheaf_equiv_of_iso C H.symm).to_adjunction

/-- Pulling back along the inverse of a homeomorphism is the same as pushing forward along it. -/
def pullback_inv_iso_pushforward_hom {X Y : Top.{v}} (H : X ‚âÖ Y) :
  pullback C H.inv ‚âÖ pushforward C H.hom :=
adjunction.left_adjoint_uniq
  (pushforward_pullback_adjunction C H.inv)
  (presheaf_equiv_of_iso C H).to_adjunction

end presheaf
end Top
