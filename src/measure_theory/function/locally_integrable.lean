/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.integral.integrable_on

/-!
# Locally integrable functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A function is called *locally integrable* (`measure_theory.locally_integrable`) if it is integrable
on a neighborhood of every point. More generally, it is *locally integrable on `s`* if it is
locally integrable on a neighbourhood within `s` of any point of `s`.

This file contains properties of locally integrable functions, and integrability results
on compact sets.

## Main statements

* `continuous.locally_integrable`: A continuous function is locally integrable.
* `continuous_on.locally_integrable_on`: A function which is continuous on `s` is locally
  integrable on `s`.
-/

open measure_theory measure_theory.measure set function topological_space
open_locale topology interval

variables {X Y E R : Type*} [measurable_space X] [topological_space X]
variables [measurable_space Y] [topological_space Y]
variables [normed_add_comm_group E] {f : X ‚Üí E} {Œº : measure X} {s : set X}

namespace measure_theory

section locally_integrable_on

/-- A function `f : X ‚Üí E` is *locally integrable on s*, for `s ‚äÜ X`, if for every `x ‚àà s` there is
a neighbourhood of `x` within `s` on which `f` is integrable. (Note this is, in general, strictly
weaker than local integrability with respect to `Œº.restrict s`.) -/
def locally_integrable_on (f : X ‚Üí E) (s : set X) (Œº : measure X . volume_tac) : Prop :=
‚àÄ (x : X), x ‚àà s ‚Üí integrable_at_filter f (ùìù[s] x) Œº

lemma locally_integrable_on.mono
  (hf : measure_theory.locally_integrable_on f s Œº) {t : set X} (hst : t ‚äÜ s) :
  locally_integrable_on f t Œº :=
Œª x hx, (hf x $ hst hx).filter_mono (nhds_within_mono x hst)

lemma locally_integrable_on.norm (hf : locally_integrable_on f s Œº) :
  locally_integrable_on (Œª x, ‚Äñf x‚Äñ) s Œº :=
Œª t ht, let ‚ü®U, hU_nhd, hU_int‚ü© := hf t ht in ‚ü®U, hU_nhd, hU_int.norm‚ü©

lemma integrable_on.locally_integrable_on (hf : integrable_on f s Œº) :
  locally_integrable_on f s Œº :=
Œª x hx, ‚ü®s, self_mem_nhds_within, hf‚ü©

/-- If a function is locally integrable on a compact set, then it is integrable on that set. -/
lemma locally_integrable_on.integrable_on_is_compact
  (hf : locally_integrable_on f s Œº) (hs : is_compact s) :
  integrable_on f s Œº :=
is_compact.induction_on hs integrable_on_empty (Œª u v huv hv, hv.mono_set huv)
  (Œª u v hu hv, integrable_on_union.mpr ‚ü®hu, hv‚ü©) hf

lemma locally_integrable_on.integrable_on_compact_subset
  (hf : locally_integrable_on f s Œº) {t : set X} (hst : t ‚äÜ s) (ht : is_compact t) :
  integrable_on f t Œº :=
(hf.mono hst).integrable_on_is_compact ht

lemma locally_integrable_on.ae_strongly_measurable [second_countable_topology X]
  (hf : locally_integrable_on f s Œº) :
  ae_strongly_measurable f (Œº.restrict s) :=
begin
  have : ‚àÄ (x : s), ‚àÉ u, is_open u ‚àß x.1 ‚àà u ‚àß integrable_on f (u ‚à© s) Œº,
  { rintro ‚ü®x, hx‚ü©,
    rcases hf x hx with ‚ü®t, ht, h't‚ü©,
    rcases mem_nhds_within.1 ht with ‚ü®u, u_open, x_mem, u_sub‚ü©,
    refine ‚ü®u, u_open, x_mem, h't.mono_set u_sub‚ü© },
  choose u u_open xu hu using this,
  obtain ‚ü®T, T_count, hT‚ü© : ‚àÉ (T : set s), T.countable ‚àß s = (‚ãÉ (i : T), u i ‚à© s),
  { have : s ‚äÜ (‚ãÉ (x : s), u x), from Œª y hy, mem_Union_of_mem ‚ü®y, hy‚ü© (xu ‚ü®y, hy‚ü©),
    obtain ‚ü®T, hT_count, hT_un‚ü© := is_open_Union_countable u u_open,
    refine ‚ü®T, hT_count, _‚ü©,
    rw [‚ÜêhT_un, bUnion_eq_Union] at this,
    rw [‚ÜêUnion_inter, eq_comm, inter_eq_right_iff_subset],
    exact this },
  haveI : countable T, from countable_coe_iff.mpr T_count,
  rw [hT, ae_strongly_measurable_Union_iff],
  exact Œª (i : T), (hu i).ae_strongly_measurable,
end

/-- If `s` is either open, or closed, then `f` is locally integrable on `s` iff it is integrable on
every compact subset contained in `s`. -/
lemma locally_integrable_on_iff [locally_compact_space X] [t2_space X]
  (hs : is_closed s ‚à® is_open s) :
  locally_integrable_on f s Œº ‚Üî ‚àÄ (k : set X) (hk : k ‚äÜ s), is_compact k ‚Üí integrable_on f k Œº :=
begin
  -- The correct condition is that `s` be *locally closed*, i.e. for every `x ‚àà s` there is some
  -- `U ‚àà ùìù x` such that `U ‚à© s` is closed. But mathlib doesn't have locally closed sets yet.
  refine ‚ü®Œª hf k hk, hf.integrable_on_compact_subset hk, Œª hf x hx, _‚ü©,
  cases hs,
  { exact let ‚ü®K, hK, h2K‚ü© := exists_compact_mem_nhds x in ‚ü®_, inter_mem_nhds_within s h2K,
    hf _ (inter_subset_left _ _) (is_compact_of_is_closed_subset hK (hs.inter hK.is_closed)
    (inter_subset_right _ _))‚ü© },
  { obtain ‚ü®K, hK, h2K, h3K‚ü© := exists_compact_subset hs hx,
    refine ‚ü®K, _, hf K h3K hK‚ü©,
    simpa only [is_open.nhds_within_eq hs hx, interior_eq_nhds'] using h2K }
end

end locally_integrable_on

/-- A function `f : X ‚Üí E` is *locally integrable* if it is integrable on a neighborhood of every
point. In particular, it is integrable on all compact sets,
see `locally_integrable.integrable_on_is_compact`. -/
def locally_integrable (f : X ‚Üí E) (Œº : measure X . volume_tac) : Prop :=
‚àÄ (x : X), integrable_at_filter f (ùìù x) Œº

lemma locally_integrable_on_univ : locally_integrable_on f univ Œº ‚Üî locally_integrable f Œº :=
by simpa only [locally_integrable_on, nhds_within_univ, mem_univ, true_implies_iff]

lemma locally_integrable.locally_integrable_on (hf : locally_integrable f Œº) (s : set X) :
  locally_integrable_on f s Œº :=
Œª x hx, (hf x).filter_mono nhds_within_le_nhds

lemma integrable.locally_integrable (hf : integrable f Œº) : locally_integrable f Œº :=
Œª x, hf.integrable_at_filter _

/-- If `f` is locally integrable with respect to `Œº.restrict s`, it is locally integrable on `s`.
(See `locally_integrable_on_iff_locally_integrable_restrict` for an iff statement when `s` is
closed.) -/
lemma locally_integrable_on_of_locally_integrable_restrict [opens_measurable_space X]
  (hf : locally_integrable f (Œº.restrict s)) :
  locally_integrable_on f s Œº :=
begin
  intros x hx,
  obtain ‚ü®t, ht_mem, ht_int‚ü© := hf x,
  obtain ‚ü®u, hu_sub, hu_o, hu_mem‚ü© := mem_nhds_iff.mp ht_mem,
  refine ‚ü®_, inter_mem_nhds_within s (hu_o.mem_nhds hu_mem), _‚ü©,
  simpa only [integrable_on, measure.restrict_restrict hu_o.measurable_set, inter_comm]
    using ht_int.mono_set hu_sub,
end

/-- If `s` is closed, being locally integrable on `s` wrt `Œº` is equivalent to being locally
integrable with respect to `Œº.restrict s`. For the one-way implication without assuming `s` closed,
see `locally_integrable_on_of_locally_integrable_restrict`. -/
lemma locally_integrable_on_iff_locally_integrable_restrict [opens_measurable_space X]
  (hs : is_closed s) :
  locally_integrable_on f s Œº ‚Üî locally_integrable f (Œº.restrict s) :=
begin
  refine ‚ü®Œª hf x, _, locally_integrable_on_of_locally_integrable_restrict‚ü©,
  by_cases h : x ‚àà s,
  { obtain ‚ü®t, ht_nhds, ht_int‚ü© := hf x h,
    obtain ‚ü®u, hu_o, hu_x, hu_sub‚ü© := mem_nhds_within.mp ht_nhds,
    refine ‚ü®u, hu_o.mem_nhds hu_x, _‚ü©,
    rw [integrable_on, restrict_restrict hu_o.measurable_set],
    exact ht_int.mono_set hu_sub },
  { rw ‚Üêis_open_compl_iff at hs,
    refine ‚ü®s·∂ú, hs.mem_nhds h, _‚ü©,
    rw [integrable_on, restrict_restrict, inter_comm, inter_compl_self, ‚Üêintegrable_on],
    exacts [integrable_on_empty, hs.measurable_set] },
end

/-- If a function is locally integrable, then it is integrable on any compact set. -/
lemma locally_integrable.integrable_on_is_compact {k : set X} (hf : locally_integrable f Œº)
  (hk : is_compact k) : integrable_on f k Œº :=
(hf.locally_integrable_on k).integrable_on_is_compact hk

/-- If a function is locally integrable, then it is integrable on an open neighborhood of any
compact set. -/
lemma locally_integrable.integrable_on_nhds_is_compact (hf : locally_integrable f Œº) {k : set X}
  (hk : is_compact k) : ‚àÉ u, is_open u ‚àß k ‚äÜ u ‚àß integrable_on f u Œº :=
begin
  refine is_compact.induction_on hk _ _ _ _,
  { refine ‚ü®‚àÖ, is_open_empty, subset.rfl, integrable_on_empty‚ü© },
  { rintros s t hst ‚ü®u, u_open, tu, hu‚ü©,
    exact ‚ü®u, u_open, hst.trans tu, hu‚ü© },
  { rintros s t ‚ü®u, u_open, su, hu‚ü© ‚ü®v, v_open, tv, hv‚ü©,
    exact ‚ü®u ‚à™ v, u_open.union v_open, union_subset_union su tv, hu.union hv‚ü© },
  { assume x hx,
    rcases hf x with ‚ü®u, ux, hu‚ü©,
    rcases mem_nhds_iff.1 ux with ‚ü®v, vu, v_open, xv‚ü©,
    exact ‚ü®v, nhds_within_le_nhds (v_open.mem_nhds xv), v, v_open, subset.rfl, hu.mono_set vu‚ü© }
end

lemma locally_integrable_iff [locally_compact_space X] :
  locally_integrable f Œº ‚Üî ‚àÄ (k : set X), is_compact k ‚Üí integrable_on f k Œº :=
‚ü®Œª hf k hk, hf.integrable_on_is_compact hk,
  Œª hf x, let ‚ü®K, hK, h2K‚ü© := exists_compact_mem_nhds x in ‚ü®K, h2K, hf K hK‚ü©‚ü©

lemma locally_integrable.ae_strongly_measurable [second_countable_topology X]
  (hf : locally_integrable f Œº) : ae_strongly_measurable f Œº :=
by simpa only [restrict_univ] using (locally_integrable_on_univ.mpr hf).ae_strongly_measurable

lemma locally_integrable_const [is_locally_finite_measure Œº] (c : E) :
  locally_integrable (Œª x, c) Œº :=
begin
  assume x,
  rcases Œº.finite_at_nhds x with ‚ü®U, hU, h'U‚ü©,
  refine ‚ü®U, hU, _‚ü©,
  simp only [h'U, integrable_on_const, or_true],
end

lemma locally_integrable_on_const [is_locally_finite_measure Œº] (c : E) :
  locally_integrable_on (Œª x, c) s Œº :=
(locally_integrable_const c).locally_integrable_on s

lemma locally_integrable.indicator (hf : locally_integrable f Œº)
  {s : set X} (hs : measurable_set s) : locally_integrable (s.indicator f) Œº :=
begin
  assume x,
  rcases hf x with ‚ü®U, hU, h'U‚ü©,
  exact ‚ü®U, hU, h'U.indicator hs‚ü©,
end

theorem locally_integrable_map_homeomorph [borel_space X] [borel_space Y]
  (e : X ‚âÉ‚Çú Y) {f : Y ‚Üí E} {Œº : measure X} :
  locally_integrable f (measure.map e Œº) ‚Üî locally_integrable (f ‚àò e) Œº :=
begin
  refine ‚ü®Œª h x, _, Œª h x, _‚ü©,
  { rcases h (e x) with ‚ü®U, hU, h'U‚ü©,
    refine ‚ü®e ‚Åª¬π' U, e.continuous.continuous_at.preimage_mem_nhds hU, _‚ü©,
    exact (integrable_on_map_equiv e.to_measurable_equiv).1 h'U },
  { rcases h (e.symm x) with ‚ü®U, hU, h'U‚ü©,
    refine ‚ü®e.symm ‚Åª¬π' U, e.symm.continuous.continuous_at.preimage_mem_nhds hU, _‚ü©,
    apply (integrable_on_map_equiv e.to_measurable_equiv).2,
    simp only [homeomorph.to_measurable_equiv_coe],
    convert h'U,
    ext x,
    simp only [mem_preimage, homeomorph.symm_apply_apply] }
end

end measure_theory

open measure_theory

section borel

variables [opens_measurable_space X] [is_locally_finite_measure Œº]
variables {K : set X} {a b : X}

/-- A continuous function `f` is locally integrable with respect to any locally finite measure. -/
lemma continuous.locally_integrable [second_countable_topology_either X E]
  (hf : continuous f) : locally_integrable f Œº :=
hf.integrable_at_nhds

/-- A function `f` continuous on a set `K` is locally integrable on this set with respect
to any locally finite measure. -/
lemma continuous_on.locally_integrable_on [second_countable_topology_either X E]
  (hf : continuous_on f K) (hK : measurable_set K) :
  locally_integrable_on f K Œº :=
Œª x hx, hf.integrable_at_nhds_within hK hx

variables [metrizable_space X]

/-- A function `f` continuous on a compact set `K` is integrable on this set with respect to any
locally finite measure. -/
lemma continuous_on.integrable_on_compact (hK : is_compact K) (hf : continuous_on f K) :
  integrable_on f K Œº :=
begin
  letI := metrizable_space_metric X,
  refine locally_integrable_on.integrable_on_is_compact (Œª x hx, _) hK,
  exact hf.integrable_at_nhds_within_of_is_separable hK.measurable_set hK.is_separable hx,
end

lemma continuous_on.integrable_on_Icc [preorder X] [compact_Icc_space X]
  (hf : continuous_on f (Icc a b)) : integrable_on f (Icc a b) Œº :=
hf.integrable_on_compact is_compact_Icc

lemma continuous.integrable_on_Icc [preorder X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f (Icc a b) Œº :=
hf.continuous_on.integrable_on_Icc

lemma continuous.integrable_on_Ioc [preorder X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f (Ioc a b) Œº :=
hf.integrable_on_Icc.mono_set Ioc_subset_Icc_self

lemma continuous_on.integrable_on_uIcc [linear_order X] [compact_Icc_space X]
  (hf : continuous_on f [a, b]) : integrable_on f [a, b] Œº :=
hf.integrable_on_Icc

lemma continuous.integrable_on_uIcc [linear_order X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f [a, b] Œº :=
hf.integrable_on_Icc

lemma continuous.integrable_on_uIoc [linear_order X] [compact_Icc_space X]
  (hf : continuous f) : integrable_on f (Œô a b) Œº :=
hf.integrable_on_Ioc

/-- A continuous function with compact support is integrable on the whole space. -/
lemma continuous.integrable_of_has_compact_support
  (hf : continuous f) (hcf : has_compact_support f) : integrable f Œº :=
(integrable_on_iff_integrable_of_support_subset (subset_tsupport f)).mp $
  hf.continuous_on.integrable_on_compact hcf

end borel

open_locale ennreal

section monotone

variables [borel_space X]
  [conditionally_complete_linear_order X] [conditionally_complete_linear_order E]
  [order_topology X] [order_topology E] [second_countable_topology E]

lemma monotone_on.integrable_on_of_measure_ne_top
  (hmono : monotone_on f s) {a b : X} (ha : is_least s a) (hb : is_greatest s b) (hs : Œº s ‚â† ‚àû)
  (h's : measurable_set s) :
  integrable_on f s Œº :=
begin
  borelize E,
  obtain rfl | h := s.eq_empty_or_nonempty,
  { exact integrable_on_empty },
  have hbelow : bdd_below (f '' s) := ‚ü®f a, Œª x ‚ü®y, hy, hyx‚ü©, hyx ‚ñ∏ hmono ha.1 hy (ha.2 hy)‚ü©,
  have habove : bdd_above (f '' s) := ‚ü®f b, Œª x ‚ü®y, hy, hyx‚ü©, hyx ‚ñ∏ hmono hy hb.1 (hb.2 hy)‚ü©,
  have : metric.bounded (f '' s) := metric.bounded_of_bdd_above_of_bdd_below habove hbelow,
  rcases bounded_iff_forall_norm_le.mp this with ‚ü®C, hC‚ü©,
  have A : integrable_on (Œª x, C) s Œº, by simp only [hs.lt_top, integrable_on_const, or_true],
  refine integrable.mono' A
    (ae_measurable_restrict_of_monotone_on h's hmono).ae_strongly_measurable
    ((ae_restrict_iff' h's).mpr $ ae_of_all _ $
      Œª y hy, hC (f y) (mem_image_of_mem f hy)),
end

lemma monotone_on.integrable_on_is_compact [is_finite_measure_on_compacts Œº]
  (hs : is_compact s) (hmono : monotone_on f s) :
  integrable_on f s Œº :=
begin
  obtain rfl | h := s.eq_empty_or_nonempty,
  { exact integrable_on_empty },
  { exact hmono.integrable_on_of_measure_ne_top (hs.is_least_Inf h) (hs.is_greatest_Sup h)
    hs.measure_lt_top.ne hs.measurable_set }
end

lemma antitone_on.integrable_on_of_measure_ne_top
  (hanti : antitone_on f s) {a b : X} (ha : is_least s a) (hb : is_greatest s b) (hs : Œº s ‚â† ‚àû)
  (h's : measurable_set s) :
  integrable_on f s Œº :=
hanti.dual_right.integrable_on_of_measure_ne_top ha hb  hs h's

lemma antione_on.integrable_on_is_compact [is_finite_measure_on_compacts Œº]
  (hs : is_compact s) (hanti : antitone_on f s) :
  integrable_on f s Œº :=
hanti.dual_right.integrable_on_is_compact hs

lemma monotone.locally_integrable [is_locally_finite_measure Œº] (hmono : monotone f) :
  locally_integrable f Œº :=
begin
  assume x,
  rcases Œº.finite_at_nhds x with ‚ü®U, hU, h'U‚ü©,
  obtain ‚ü®a, b, xab, hab, abU‚ü© : ‚àÉ (a b : X), x ‚àà Icc a b ‚àß Icc a b ‚àà ùìù x ‚àß Icc a b ‚äÜ U,
    from exists_Icc_mem_subset_of_mem_nhds hU,
  have ab : a ‚â§ b := xab.1.trans xab.2,
  refine ‚ü®Icc a b, hab, _‚ü©,
  exact (hmono.monotone_on _).integrable_on_of_measure_ne_top (is_least_Icc ab)
    (is_greatest_Icc ab) ((measure_mono abU).trans_lt h'U).ne measurable_set_Icc,
end

lemma antitone.locally_integrable [is_locally_finite_measure Œº] (hanti : antitone f) :
  locally_integrable f Œº :=
hanti.dual_right.locally_integrable

end monotone

namespace measure_theory

variables [opens_measurable_space X] {A K : set X}

section mul

variables [normed_ring R] [second_countable_topology_either X R] {g g' : X ‚Üí R}

lemma integrable_on.mul_continuous_on_of_subset
  (hg : integrable_on g A Œº) (hg' : continuous_on g' K)
  (hA : measurable_set A) (hK : is_compact K) (hAK : A ‚äÜ K) :
  integrable_on (Œª x, g x * g' x) A Œº :=
begin
  rcases is_compact.exists_bound_of_continuous_on hK hg' with ‚ü®C, hC‚ü©,
  rw [integrable_on, ‚Üê mem_‚Ñíp_one_iff_integrable] at hg ‚ä¢,
  have : ‚àÄ·µê x ‚àÇ(Œº.restrict A), ‚Äñg x * g' x‚Äñ ‚â§ C * ‚Äñg x‚Äñ,
  { filter_upwards [ae_restrict_mem hA] with x hx,
    refine (norm_mul_le _ _).trans _,
    rw mul_comm,
    apply mul_le_mul_of_nonneg_right (hC x (hAK hx)) (norm_nonneg _), },
  exact mem_‚Ñíp.of_le_mul hg (hg.ae_strongly_measurable.mul $
    (hg'.mono hAK).ae_strongly_measurable hA) this,
end

lemma integrable_on.mul_continuous_on [t2_space X]
  (hg : integrable_on g K Œº) (hg' : continuous_on g' K) (hK : is_compact K) :
  integrable_on (Œª x, g x * g' x) K Œº :=
hg.mul_continuous_on_of_subset hg' hK.measurable_set hK (subset.refl _)

lemma integrable_on.continuous_on_mul_of_subset
  (hg : continuous_on g K) (hg' : integrable_on g' A Œº)
  (hK : is_compact K) (hA : measurable_set A) (hAK : A ‚äÜ K) :
  integrable_on (Œª x, g x * g' x) A Œº :=
begin
  rcases is_compact.exists_bound_of_continuous_on hK hg with ‚ü®C, hC‚ü©,
  rw [integrable_on, ‚Üê mem_‚Ñíp_one_iff_integrable] at hg' ‚ä¢,
  have : ‚àÄ·µê x ‚àÇ(Œº.restrict A), ‚Äñg x * g' x‚Äñ ‚â§ C * ‚Äñg' x‚Äñ,
  { filter_upwards [ae_restrict_mem hA] with x hx,
    refine (norm_mul_le _ _).trans _,
    apply mul_le_mul_of_nonneg_right (hC x (hAK hx)) (norm_nonneg _), },
  exact mem_‚Ñíp.of_le_mul hg' (((hg.mono hAK).ae_strongly_measurable hA).mul
    hg'.ae_strongly_measurable) this,
end

lemma integrable_on.continuous_on_mul [t2_space X]
  (hg : continuous_on g K) (hg' : integrable_on g' K Œº) (hK : is_compact K) :
  integrable_on (Œª x, g x * g' x) K Œº :=
hg'.continuous_on_mul_of_subset hg hK hK.measurable_set subset.rfl

end mul

section smul

variables {ùïú : Type*} [normed_field ùïú] [normed_space ùïú E]

lemma integrable_on.continuous_on_smul [t2_space X] [second_countable_topology_either X ùïú]
  {g : X ‚Üí E} (hg : integrable_on g K Œº) {f : X ‚Üí ùïú} (hf : continuous_on f K) (hK : is_compact K) :
  integrable_on (Œª x, f x ‚Ä¢ g x) K Œº :=
begin
  rw [integrable_on, ‚Üêintegrable_norm_iff],
  { simp_rw norm_smul,
    refine integrable_on.continuous_on_mul _ hg.norm hK,
    exact continuous_norm.comp_continuous_on hf },
  { exact (hf.ae_strongly_measurable hK.measurable_set).smul hg.1 }
end

lemma integrable_on.smul_continuous_on [t2_space X] [second_countable_topology_either X E]
  {f : X ‚Üí ùïú} (hf : integrable_on f K Œº) {g : X ‚Üí E} (hg : continuous_on g K) (hK : is_compact K) :
  integrable_on (Œª x, f x ‚Ä¢ g x) K Œº :=
begin
  rw [integrable_on, ‚Üêintegrable_norm_iff],
  { simp_rw norm_smul,
    refine integrable_on.mul_continuous_on hf.norm _ hK,
    exact continuous_norm.comp_continuous_on hg },
  { exact hf.1.smul (hg.ae_strongly_measurable hK.measurable_set) }
end

end smul

namespace locally_integrable_on

lemma continuous_on_mul [locally_compact_space X] [t2_space X] [normed_ring R]
  [second_countable_topology_either X R]
  {f g : X ‚Üí R} {s : set X}
  (hf : locally_integrable_on f s Œº) (hg : continuous_on g s) (hs : is_open s) :
  locally_integrable_on (Œª x, g x * f x) s Œº :=
begin
  rw measure_theory.locally_integrable_on_iff (or.inr hs) at hf ‚ä¢,
  exact Œª k hk_sub hk_c, (hf k hk_sub hk_c).continuous_on_mul (hg.mono hk_sub) hk_c
end

lemma mul_continuous_on [locally_compact_space X] [t2_space X] [normed_ring R]
  [second_countable_topology_either X R] {f g : X ‚Üí R} {s : set X}
  (hf : locally_integrable_on f s Œº) (hg : continuous_on g s) (hs : is_open s) :
  locally_integrable_on (Œª x, f x * g x) s Œº :=
begin
  rw measure_theory.locally_integrable_on_iff (or.inr hs) at hf ‚ä¢,
  exact Œª k hk_sub hk_c, (hf k hk_sub hk_c).mul_continuous_on (hg.mono hk_sub) hk_c
end

lemma continuous_on_smul [locally_compact_space X] [t2_space X]
  {ùïú : Type*} [normed_field ùïú] [second_countable_topology_either X ùïú] [normed_space ùïú E]
  {f : X ‚Üí E} {g : X ‚Üí ùïú} {s : set X} (hs : is_open s)
  (hf : locally_integrable_on f s Œº) (hg : continuous_on g s) :
  locally_integrable_on (Œª x, g x ‚Ä¢ f x) s Œº :=
begin
  rw measure_theory.locally_integrable_on_iff (or.inr hs) at hf ‚ä¢,
  exact Œª k hk_sub hk_c, (hf k hk_sub hk_c).continuous_on_smul (hg.mono hk_sub) hk_c
end

lemma smul_continuous_on [locally_compact_space X] [t2_space X]
  {ùïú : Type*} [normed_field ùïú] [second_countable_topology_either X E] [normed_space ùïú E]
  {f : X ‚Üí ùïú} {g : X ‚Üí E} {s : set X} (hs : is_open s)
  (hf : locally_integrable_on f s Œº) (hg : continuous_on g s)  :
  locally_integrable_on (Œª x, f x ‚Ä¢ g x) s Œº :=
begin
  rw measure_theory.locally_integrable_on_iff (or.inr hs) at hf ‚ä¢,
  exact Œª k hk_sub hk_c, (hf k hk_sub hk_c).smul_continuous_on (hg.mono hk_sub) hk_c
end

end locally_integrable_on

end measure_theory
