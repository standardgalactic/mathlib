/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, R√©my Degenne
-/
import probability.process.filtration
import topology.instances.discrete

/-!
# Adapted and progressively measurable processes

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines some standard definition from the theory of stochastic processes including
filtrations and stopping times. These definitions are used to model the amount of information
at a specific time and are the first step in formalizing stochastic processes.

## Main definitions

* `measure_theory.adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-strongly measurable
* `measure_theory.prog_measurable`: a sequence of functions `u` is said to be progressively
  measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
  `set.Iic i √ó Œ©` is strongly measurable with respect to the product `measurable_space` structure
  where the œÉ-algebra used for `Œ©` is `f i`.

## Main results

* `adapted.prog_measurable_of_continuous`: a continuous adapted process is progressively measurable.

## Tags

adapted, progressively measurable

-/

open filter order topological_space
open_locale classical measure_theory nnreal ennreal topology big_operators

namespace measure_theory

variables {Œ© Œ≤ Œπ : Type*} {m : measurable_space Œ©} [topological_space Œ≤] [preorder Œπ]
  {u v : Œπ ‚Üí Œ© ‚Üí Œ≤} {f : filtration Œπ m}

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def adapted (f : filtration Œπ m) (u : Œπ ‚Üí Œ© ‚Üí Œ≤) : Prop :=
‚àÄ i : Œπ, strongly_measurable[f i] (u i)

namespace adapted

@[protected, to_additive] lemma mul [has_mul Œ≤] [has_continuous_mul Œ≤]
  (hu : adapted f u) (hv : adapted f v) :
  adapted f (u * v) :=
Œª i, (hu i).mul (hv i)

@[protected, to_additive] lemma div [has_div Œ≤] [has_continuous_div Œ≤]
  (hu : adapted f u) (hv : adapted f v) :
  adapted f (u / v) :=
Œª i, (hu i).div (hv i)

@[protected, to_additive] lemma inv [group Œ≤] [topological_group Œ≤] (hu : adapted f u) :
  adapted f u‚Åª¬π :=
Œª i, (hu i).inv

@[protected] lemma smul [has_smul ‚Ñù Œ≤] [has_continuous_smul ‚Ñù Œ≤] (c : ‚Ñù) (hu : adapted f u) :
  adapted f (c ‚Ä¢ u) :=
Œª i, (hu i).const_smul c

@[protected] lemma strongly_measurable {i : Œπ} (hf : adapted f u) :
  strongly_measurable[m] (u i) :=
(hf i).mono (f.le i)

lemma strongly_measurable_le {i j : Œπ} (hf : adapted f u) (hij : i ‚â§ j) :
  strongly_measurable[f j] (u i) :=
(hf i).mono (f.mono hij)

end adapted

lemma adapted_const (f : filtration Œπ m) (x : Œ≤) : adapted f (Œª _ _, x) :=
Œª i, strongly_measurable_const

variable (Œ≤)
lemma adapted_zero [has_zero Œ≤] (f : filtration Œπ m) : adapted f (0 : Œπ ‚Üí Œ© ‚Üí Œ≤) :=
Œª i, @strongly_measurable_zero Œ© Œ≤ (f i) _ _
variable {Œ≤}

lemma filtration.adapted_natural [metrizable_space Œ≤] [mŒ≤ : measurable_space Œ≤] [borel_space Œ≤]
  {u : Œπ ‚Üí Œ© ‚Üí Œ≤} (hum : ‚àÄ i, strongly_measurable[m] (u i)) :
  adapted (filtration.natural u hum) u :=
begin
  assume i,
  refine strongly_measurable.mono _ (le_supr‚ÇÇ_of_le i (le_refl i) le_rfl),
  rw strongly_measurable_iff_measurable_separable,
  exact ‚ü®measurable_iff_comap_le.2 le_rfl, (hum i).is_separable_range‚ü©
end

/-- Progressively measurable process. A sequence of functions `u` is said to be progressively
measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
`set.Iic i √ó Œ©` is measurable with respect to the product `measurable_space` structure where the
œÉ-algebra used for `Œ©` is `f i`.
The usual definition uses the interval `[0,i]`, which we replace by `set.Iic i`. We recover the
usual definition for index types `‚Ñù‚â•0` or `‚Ñï`. -/
def prog_measurable [measurable_space Œπ] (f : filtration Œπ m) (u : Œπ ‚Üí Œ© ‚Üí Œ≤) : Prop :=
‚àÄ i, strongly_measurable[subtype.measurable_space.prod (f i)] (Œª p : set.Iic i √ó Œ©, u p.1 p.2)

lemma prog_measurable_const [measurable_space Œπ] (f : filtration Œπ m) (b : Œ≤) :
  prog_measurable f ((Œª _ _, b) : Œπ ‚Üí Œ© ‚Üí Œ≤) :=
Œª i, @strongly_measurable_const _ _ (subtype.measurable_space.prod (f i)) _ _

namespace prog_measurable

variables [measurable_space Œπ]

protected lemma adapted (h : prog_measurable f u) : adapted f u :=
begin
  intro i,
  have : u i = (Œª p : set.Iic i √ó Œ©, u p.1 p.2) ‚àò (Œª x, (‚ü®i, set.mem_Iic.mpr le_rfl‚ü©, x)) := rfl,
  rw this,
  exact (h i).comp_measurable measurable_prod_mk_left,
end

protected lemma comp {t : Œπ ‚Üí Œ© ‚Üí Œπ} [topological_space Œπ] [borel_space Œπ] [metrizable_space Œπ]
  (h : prog_measurable f u) (ht : prog_measurable f t)
  (ht_le : ‚àÄ i œâ, t i œâ ‚â§ i) :
  prog_measurable f (Œª i œâ, u (t i œâ) œâ) :=
begin
  intro i,
  have : (Œª p : ‚Ü•(set.Iic i) √ó Œ©, u (t (p.fst : Œπ) p.snd) p.snd)
    = (Œª p : ‚Ü•(set.Iic i) √ó Œ©, u (p.fst : Œπ) p.snd) ‚àò (Œª p : ‚Ü•(set.Iic i) √ó Œ©,
      (‚ü®t (p.fst : Œπ) p.snd, set.mem_Iic.mpr ((ht_le _ _).trans p.fst.prop)‚ü©, p.snd)) := rfl,
  rw this,
  exact (h i).comp_measurable ((ht i).measurable.subtype_mk.prod_mk measurable_snd),
end

section arithmetic

@[to_additive] protected lemma mul [has_mul Œ≤] [has_continuous_mul Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i œâ, u i œâ * v i œâ) :=
Œª i, (hu i).mul (hv i)

@[to_additive] protected lemma finset_prod' {Œ≥} [comm_monoid Œ≤] [has_continuous_mul Œ≤]
  {U : Œ≥ ‚Üí Œπ ‚Üí Œ© ‚Üí Œ≤} {s : finset Œ≥} (h : ‚àÄ c ‚àà s, prog_measurable f (U c)) :
  prog_measurable f (‚àè c in s, U c) :=
finset.prod_induction U (prog_measurable f) (Œª _ _, prog_measurable.mul)
  (prog_measurable_const _ 1) h

@[to_additive] protected lemma finset_prod {Œ≥} [comm_monoid Œ≤] [has_continuous_mul Œ≤]
  {U : Œ≥ ‚Üí Œπ ‚Üí Œ© ‚Üí Œ≤} {s : finset Œ≥} (h : ‚àÄ c ‚àà s, prog_measurable f (U c)) :
  prog_measurable f (Œª i a, ‚àè c in s, U c i a) :=
by { convert prog_measurable.finset_prod' h, ext i a, simp only [finset.prod_apply], }

@[to_additive] protected lemma inv [group Œ≤] [topological_group Œ≤] (hu : prog_measurable f u) :
  prog_measurable f (Œª i œâ, (u i œâ)‚Åª¬π) :=
Œª i, (hu i).inv

@[to_additive] protected lemma div [group Œ≤] [topological_group Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i œâ, u i œâ / v i œâ) :=
Œª i, (hu i).div (hv i)

end arithmetic

end prog_measurable

lemma prog_measurable_of_tendsto' {Œ≥} [measurable_space Œπ] [pseudo_metrizable_space Œ≤]
  (fltr : filter Œ≥) [fltr.ne_bot] [fltr.is_countably_generated] {U : Œ≥ ‚Üí Œπ ‚Üí Œ© ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U fltr (ùìù u)) :
  prog_measurable f u :=
begin
  assume i,
  apply @strongly_measurable_of_tendsto (set.Iic i √ó Œ©) Œ≤ Œ≥ (measurable_space.prod _ (f i))
   _ _ fltr _ _ _ _ (Œª l, h l i),
  rw tendsto_pi_nhds at h_tendsto ‚ä¢,
  intro x,
  specialize h_tendsto x.fst,
  rw tendsto_nhds at h_tendsto ‚ä¢,
  exact Œª s hs h_mem, h_tendsto {g | g x.snd ‚àà s} (hs.preimage (continuous_apply x.snd)) h_mem,
end

lemma prog_measurable_of_tendsto [measurable_space Œπ] [pseudo_metrizable_space Œ≤]
  {U : ‚Ñï ‚Üí Œπ ‚Üí Œ© ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U at_top (ùìù u)) :
  prog_measurable f u :=
prog_measurable_of_tendsto' at_top h h_tendsto

/-- A continuous and adapted process is progressively measurable. -/
theorem adapted.prog_measurable_of_continuous
  [topological_space Œπ] [metrizable_space Œπ] [second_countable_topology Œπ]
  [measurable_space Œπ] [opens_measurable_space Œπ]
  [pseudo_metrizable_space Œ≤]
  (h : adapted f u) (hu_cont : ‚àÄ œâ, continuous (Œª i, u i œâ)) :
  prog_measurable f u :=
Œª i, @strongly_measurable_uncurry_of_continuous_of_strongly_measurable _ _ (set.Iic i) _ _ _ _ _ _ _
  (f i) _ (Œª œâ, (hu_cont œâ).comp continuous_induced_dom) (Œª j, (h j).mono (f.mono j.prop))

/-- For filtrations indexed by a discrete order, `adapted` and `prog_measurable` are equivalent.
This lemma provides `adapted f u ‚Üí prog_measurable f u`.
See `prog_measurable.adapted` for the reverse direction, which is true more generally. -/
lemma adapted.prog_measurable_of_discrete [topological_space Œπ] [discrete_topology Œπ]
  [second_countable_topology Œπ] [measurable_space Œπ] [opens_measurable_space Œπ]
  [pseudo_metrizable_space Œ≤]
  (h : adapted f u) :
  prog_measurable f u :=
h.prog_measurable_of_continuous (Œª _, continuous_of_discrete_topology)

-- this dot notation will make more sense once we have a more general definition for predictable
lemma predictable.adapted {f : filtration ‚Ñï m} {u : ‚Ñï ‚Üí Œ© ‚Üí Œ≤}
  (hu : adapted f (Œª n, u (n + 1))) (hu0 : strongly_measurable[f 0] (u 0)) :
  adapted f u :=
Œª n, match n with
  | 0 := hu0
  | n + 1 := (hu n).mono (f.mono n.le_succ)
end

end measure_theory
