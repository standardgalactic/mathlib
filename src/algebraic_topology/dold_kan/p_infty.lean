/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import algebraic_topology.dold_kan.projections
import category_theory.idempotents.functor_categories
import category_theory.idempotents.functor_extension

/-!

# Construction of the projection `P_infty` for the Dold-Kan correspondence

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

TODO (@joelriou) continue adding the various files referenced below

In this file, we construct the projection `P_infty : K[X] ‚ü∂ K[X]` by passing
to the limit the projections `P q` defined in `projections.lean`. This
projection is a critical tool in this formalisation of the Dold-Kan correspondence,
because in the case of abelian categories, `P_infty` corresponds to the
projection on the normalized Moore subcomplex, with kernel the degenerate subcomplex.
(See `equivalence.lean` for the general strategy of proof.)

-/

open category_theory
open category_theory.category
open category_theory.preadditive
open category_theory.simplicial_object
open category_theory.idempotents
open opposite
open_locale simplicial dold_kan

noncomputable theory

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [preadditive C] {X : simplicial_object C}

lemma P_is_eventually_constant {q n : ‚Ñï} (hqn : n ‚â§ q) :
  ((P (q+1)).f n : X _[n] ‚ü∂ _ ) = (P q).f n :=
begin
  cases n,
  { simp only [P_f_0_eq], },
  { unfold P,
    simp only [add_right_eq_self, comp_add, homological_complex.comp_f,
      homological_complex.add_f_apply, comp_id],
    exact (higher_faces_vanish.of_P q n).comp_HœÉ_eq_zero
      (nat.succ_le_iff.mp hqn), },
end

lemma Q_is_eventually_constant {q n : ‚Ñï} (hqn : n ‚â§ q) :
  ((Q (q+1)).f n : X _[n] ‚ü∂ _ ) = (Q q).f n :=
by simp only [Q, homological_complex.sub_f_apply, P_is_eventually_constant hqn]

/-- The endomorphism `P_infty : K[X] ‚ü∂ K[X]` obtained from the `P q` by passing to the limit. -/
def P_infty : K[X] ‚ü∂ K[X] := chain_complex.of_hom _ _ _ _ _ _
  (Œª n, ((P n).f n : X _[n] ‚ü∂ _ ))
  (Œª n, by simpa only [‚Üê P_is_eventually_constant (show n ‚â§ n, by refl),
    alternating_face_map_complex.obj_d_eq] using (P (n+1)).comm (n+1) n)

/-- The endomorphism `Q_infty : K[X] ‚ü∂ K[X]` obtained from the `Q q` by passing to the limit. -/
def Q_infty : K[X] ‚ü∂ K[X] := ùüô _ - P_infty

@[simp]
lemma P_infty_f_0 : (P_infty.f 0 : X _[0] ‚ü∂ X _[0]) = ùüô _ := rfl

lemma P_infty_f (n : ‚Ñï) : (P_infty.f n : X _[n] ‚ü∂  X _[n] ) = (P n).f n := rfl

@[simp]
lemma Q_infty_f_0 : (Q_infty.f 0 : X _[0] ‚ü∂ X _[0]) = 0 :=
by { dsimp [Q_infty], simp only [sub_self], }

lemma Q_infty_f (n : ‚Ñï) : (Q_infty.f n : X _[n] ‚ü∂  X _[n] ) = (Q n).f n := rfl

@[simp, reassoc]
lemma P_infty_f_naturality (n : ‚Ñï) {X Y : simplicial_object C} (f : X ‚ü∂ Y) :
  f.app (op [n]) ‚â´ P_infty.f n = P_infty.f n ‚â´ f.app (op [n]) :=
P_f_naturality n n f

@[simp, reassoc]
lemma Q_infty_f_naturality (n : ‚Ñï) {X Y : simplicial_object C} (f : X ‚ü∂ Y) :
  f.app (op [n]) ‚â´ Q_infty.f n = Q_infty.f n ‚â´ f.app (op [n]) :=
Q_f_naturality n n f

@[simp, reassoc]
lemma P_infty_f_idem (n : ‚Ñï) :
  (P_infty.f n : X _[n] ‚ü∂ _) ‚â´ (P_infty.f n) = P_infty.f n :=
by simp only [P_infty_f, P_f_idem]

@[simp, reassoc]
lemma P_infty_idem : (P_infty : K[X] ‚ü∂ _) ‚â´ P_infty = P_infty :=
by { ext n, exact P_infty_f_idem n, }

@[simp, reassoc]
lemma Q_infty_f_idem (n : ‚Ñï) :
  (Q_infty.f n : X _[n] ‚ü∂ _) ‚â´ (Q_infty.f n) = Q_infty.f n :=
Q_f_idem _ _

@[simp, reassoc]
lemma Q_infty_idem : (Q_infty : K[X] ‚ü∂ _) ‚â´ Q_infty = Q_infty :=
by { ext n, exact Q_infty_f_idem n, }

@[simp, reassoc]
lemma P_infty_f_comp_Q_infty_f (n : ‚Ñï) :
  (P_infty.f n : X _[n] ‚ü∂ _) ‚â´ Q_infty.f n = 0 :=
begin
  dsimp only [Q_infty],
  simp only [homological_complex.sub_f_apply, homological_complex.id_f, comp_sub, comp_id,
    P_infty_f_idem, sub_self],
end

@[simp, reassoc]
lemma P_infty_comp_Q_infty :
  (P_infty : K[X] ‚ü∂ _) ‚â´ Q_infty = 0 :=
by { ext n, apply P_infty_f_comp_Q_infty_f, }

@[simp, reassoc]
lemma Q_infty_f_comp_P_infty_f (n : ‚Ñï) :
  (Q_infty.f n : X _[n] ‚ü∂ _) ‚â´ P_infty.f n = 0 :=
begin
  dsimp only [Q_infty],
  simp only [homological_complex.sub_f_apply, homological_complex.id_f, sub_comp, id_comp,
    P_infty_f_idem, sub_self],
end

@[simp, reassoc]
lemma Q_infty_comp_P_infty :
  (Q_infty : K[X] ‚ü∂ _) ‚â´ P_infty = 0 :=
by { ext n, apply Q_infty_f_comp_P_infty_f, }

@[simp]
lemma P_infty_add_Q_infty :
  (P_infty : K[X] ‚ü∂ _) + Q_infty = ùüô _ :=
by { dsimp only [Q_infty], simp only [add_sub_cancel'_right], }

lemma P_infty_f_add_Q_infty_f (n : ‚Ñï) :
  (P_infty.f n : X _[n] ‚ü∂ _ ) + Q_infty.f n = ùüô _ :=
homological_complex.congr_hom (P_infty_add_Q_infty) n

variable (C)

/-- `P_infty` induces a natural transformation, i.e. an endomorphism of
the functor `alternating_face_map_complex C`. -/
@[simps]
def nat_trans_P_infty :
  alternating_face_map_complex C ‚ü∂ alternating_face_map_complex C :=
{ app := Œª _, P_infty,
  naturality' := Œª X Y f, by { ext n, exact P_infty_f_naturality n f, }, }

/-- The natural transformation in each degree that is induced by `nat_trans_P_infty`. -/
@[simps]
def nat_trans_P_infty_f (n : ‚Ñï) :=
nat_trans_P_infty C ‚ó´ ùüô (homological_complex.eval _ _ n)

variable {C}

@[simp]
lemma map_P_infty_f {D : Type*} [category D] [preadditive D]
  (G : C ‚•§ D) [G.additive] (X : simplicial_object C) (n : ‚Ñï) :
  (P_infty : K[((whiskering C D).obj G).obj X] ‚ü∂ _).f n =
  G.map ((P_infty : alternating_face_map_complex.obj X ‚ü∂ _).f n) :=
by simp only [P_infty_f, map_P]

/-- Given an object `Y : karoubi (simplicial_object C)`, this lemma
computes `P_infty` for the associated object in `simplicial_object (karoubi C)`
in terms of `P_infty` for `Y.X : simplicial_object C` and `Y.p`. -/
lemma karoubi_P_infty_f {Y : karoubi (simplicial_object C)} (n : ‚Ñï) :
  ((P_infty : K[(karoubi_functor_category_embedding _ _).obj Y] ‚ü∂ _).f n).f =
    Y.p.app (op [n]) ‚â´ (P_infty : K[Y.X] ‚ü∂ _).f n :=
begin
  -- We introduce P_infty endomorphisms P‚ÇÅ, P‚ÇÇ, P‚ÇÉ, P‚ÇÑ on various objects Y‚ÇÅ, Y‚ÇÇ, Y‚ÇÉ, Y‚ÇÑ.
  let Y‚ÇÅ := (karoubi_functor_category_embedding _ _).obj Y,
  let Y‚ÇÇ := Y.X,
  let Y‚ÇÉ := (((whiskering _ _).obj (to_karoubi C)).obj Y.X),
  let Y‚ÇÑ := (karoubi_functor_category_embedding _ _).obj ((to_karoubi _).obj Y.X),
  let P‚ÇÅ : K[Y‚ÇÅ] ‚ü∂ _ := P_infty,
  let P‚ÇÇ : K[Y‚ÇÇ] ‚ü∂ _ := P_infty,
  let P‚ÇÉ : K[Y‚ÇÉ] ‚ü∂ _ := P_infty,
  let P‚ÇÑ : K[Y‚ÇÑ] ‚ü∂ _ := P_infty,
  -- The statement of lemma relates P‚ÇÅ and P‚ÇÇ.
  change (P‚ÇÅ.f n).f = Y.p.app (op [n]) ‚â´ P‚ÇÇ.f n,
  -- The proof proceeds by obtaining relations h‚ÇÉ‚ÇÇ, h‚ÇÑ‚ÇÉ, h‚ÇÅ‚ÇÑ.
  have h‚ÇÉ‚ÇÇ : (P‚ÇÉ.f n).f = P‚ÇÇ.f n := karoubi.hom_ext.mp (map_P_infty_f (to_karoubi C) Y‚ÇÇ n),
  have h‚ÇÑ‚ÇÉ : P‚ÇÑ.f n = P‚ÇÉ.f n,
  { have h := functor.congr_obj (to_karoubi_comp_karoubi_functor_category_embedding _ _) Y‚ÇÇ,
    simp only [‚Üê nat_trans_P_infty_f_app],
    congr', },
  let œÑ‚ÇÅ := ùüô (karoubi_functor_category_embedding (simplex_category·µí·µñ) C),
  let œÑ‚ÇÇ := nat_trans_P_infty_f (karoubi C) n,
  let œÑ := œÑ‚ÇÅ ‚ó´ œÑ‚ÇÇ,
  have h‚ÇÅ‚ÇÑ := idempotents.nat_trans_eq œÑ Y,
  dsimp [œÑ, œÑ‚ÇÅ, œÑ‚ÇÇ, nat_trans_P_infty_f] at h‚ÇÅ‚ÇÑ,
  rw [id_comp, id_comp, comp_id, comp_id] at h‚ÇÅ‚ÇÑ,
  /- We use the three equalities h‚ÇÉ‚ÇÇ, h‚ÇÑ‚ÇÉ, h‚ÇÅ‚ÇÑ. -/
  rw [‚Üê h‚ÇÉ‚ÇÇ, ‚Üê h‚ÇÑ‚ÇÉ, h‚ÇÅ‚ÇÑ],
  simp only [karoubi_functor_category_embedding.map_app_f, karoubi.decomp_id_p_f,
    karoubi.decomp_id_i_f, karoubi.comp_f],
  let œÄ : Y‚ÇÑ ‚ü∂ Y‚ÇÑ := (to_karoubi _ ‚ãô karoubi_functor_category_embedding _ _).map Y.p,
  have eq := karoubi.hom_ext.mp (P_infty_f_naturality n œÄ),
  simp only [karoubi.comp_f] at eq,
  dsimp [œÄ] at eq,
  rw [‚Üê eq, reassoc_of (app_idem Y (op [n]))],
end

end dold_kan

end algebraic_topology
