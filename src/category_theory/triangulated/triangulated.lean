/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import category_theory.triangulated.pretriangulated

/-!
# Triangulated Categories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains the definition of triangulated categories, which are
pretriangulated categories which satisfy the octahedron axiom.

-/

noncomputable theory

namespace category_theory

open limits category preadditive pretriangulated
open_locale zero_object

variables {C : Type*} [category C] [preadditive C] [has_zero_object C] [has_shift C ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), functor.additive (shift_functor C n)] [pretriangulated C]

variables {X‚ÇÅ X‚ÇÇ X‚ÇÉ Z‚ÇÅ‚ÇÇ Z‚ÇÇ‚ÇÉ Z‚ÇÅ‚ÇÉ : C} {u‚ÇÅ‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ} {u‚ÇÇ‚ÇÉ : X‚ÇÇ ‚ü∂ X‚ÇÉ} {u‚ÇÅ‚ÇÉ : X‚ÇÅ ‚ü∂ X‚ÇÉ}
  (comm : u‚ÇÅ‚ÇÇ ‚â´ u‚ÇÇ‚ÇÉ = u‚ÇÅ‚ÇÉ)
  {v‚ÇÅ‚ÇÇ : X‚ÇÇ ‚ü∂ Z‚ÇÅ‚ÇÇ} {w‚ÇÅ‚ÇÇ : Z‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß} (h‚ÇÅ‚ÇÇ : triangle.mk u‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ w‚ÇÅ‚ÇÇ ‚àà dist_triang C)
  {v‚ÇÇ‚ÇÉ : X‚ÇÉ ‚ü∂ Z‚ÇÇ‚ÇÉ} {w‚ÇÇ‚ÇÉ : Z‚ÇÇ‚ÇÉ ‚ü∂ X‚ÇÇ‚ü¶(1 : ‚Ñ§)‚üß} (h‚ÇÇ‚ÇÉ : triangle.mk u‚ÇÇ‚ÇÉ v‚ÇÇ‚ÇÉ w‚ÇÇ‚ÇÉ ‚àà dist_triang C)
  {v‚ÇÅ‚ÇÉ : X‚ÇÉ ‚ü∂ Z‚ÇÅ‚ÇÉ} {w‚ÇÅ‚ÇÉ : Z‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß} (h‚ÇÅ‚ÇÉ : triangle.mk u‚ÇÅ‚ÇÉ v‚ÇÅ‚ÇÉ w‚ÇÅ‚ÇÉ ‚àà dist_triang C)

namespace triangulated

include comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ

/-- An octahedron is a type of datum whose existence is asserted by
the octahedron axiom (TR 4), see https://stacks.math.columbia.edu/tag/05QK -/
structure octahedron :=
(m‚ÇÅ : Z‚ÇÅ‚ÇÇ ‚ü∂ Z‚ÇÅ‚ÇÉ)
(m‚ÇÉ : Z‚ÇÅ‚ÇÉ ‚ü∂ Z‚ÇÇ‚ÇÉ)
(comm‚ÇÅ : v‚ÇÅ‚ÇÇ ‚â´ m‚ÇÅ = u‚ÇÇ‚ÇÉ ‚â´ v‚ÇÅ‚ÇÉ)
(comm‚ÇÇ : m‚ÇÅ ‚â´ w‚ÇÅ‚ÇÉ = w‚ÇÅ‚ÇÇ)
(comm‚ÇÉ : v‚ÇÅ‚ÇÉ ‚â´ m‚ÇÉ = v‚ÇÇ‚ÇÉ)
(comm‚ÇÑ : w‚ÇÅ‚ÇÉ ‚â´ u‚ÇÅ‚ÇÇ‚ü¶1‚üß' = m‚ÇÉ ‚â´ w‚ÇÇ‚ÇÉ)
(mem : triangle.mk m‚ÇÅ m‚ÇÉ (w‚ÇÇ‚ÇÉ ‚â´ v‚ÇÅ‚ÇÇ‚ü¶1‚üß') ‚àà dist_triang C)

omit comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ

instance (X : C) : nonempty (octahedron (comp_id (ùüô X)) (contractible_distinguished X)
  (contractible_distinguished X) (contractible_distinguished X)) :=
begin
  refine ‚ü®‚ü®0, 0, _, _, _, _, by convert contractible_distinguished (0 : C)‚ü©‚ü©,
  all_goals { apply subsingleton.elim, },
end

namespace octahedron

attribute [reassoc] comm‚ÇÅ comm‚ÇÇ comm‚ÇÉ comm‚ÇÑ

variables {comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ} (h : octahedron comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ)

/-- The triangle `Z‚ÇÅ‚ÇÇ ‚ü∂ Z‚ÇÅ‚ÇÉ ‚ü∂ Z‚ÇÇ‚ÇÉ ‚ü∂ Z‚ÇÅ‚ÇÇ‚ü¶1‚üß` given by an octahedron. -/
@[simps]
def triangle : triangle C := triangle.mk h.m‚ÇÅ h.m‚ÇÉ (w‚ÇÇ‚ÇÉ ‚â´ v‚ÇÅ‚ÇÇ‚ü¶1‚üß')

/-- The first morphism of triangles given by an octahedron. -/
@[simps]
def triangle_morphism‚ÇÅ : triangle.mk u‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ w‚ÇÅ‚ÇÇ ‚ü∂ triangle.mk u‚ÇÅ‚ÇÉ v‚ÇÅ‚ÇÉ w‚ÇÅ‚ÇÉ :=
{ hom‚ÇÅ := ùüô X‚ÇÅ,
  hom‚ÇÇ := u‚ÇÇ‚ÇÉ,
  hom‚ÇÉ := h.m‚ÇÅ,
  comm‚ÇÅ' := by { dsimp, rw [id_comp, comm], },
  comm‚ÇÇ' := h.comm‚ÇÅ,
  comm‚ÇÉ' := by { dsimp, simpa only [functor.map_id, comp_id] using h.comm‚ÇÇ.symm, }, }

/-- The second morphism of triangles given an octahedron. -/
@[simps]
def triangle_morphism‚ÇÇ : triangle.mk u‚ÇÅ‚ÇÉ v‚ÇÅ‚ÇÉ w‚ÇÅ‚ÇÉ ‚ü∂ triangle.mk u‚ÇÇ‚ÇÉ v‚ÇÇ‚ÇÉ w‚ÇÇ‚ÇÉ :=
{ hom‚ÇÅ := u‚ÇÅ‚ÇÇ,
  hom‚ÇÇ := ùüô X‚ÇÉ,
  hom‚ÇÉ := h.m‚ÇÉ,
  comm‚ÇÅ' := by { dsimp, rw [comp_id, comm], },
  comm‚ÇÇ' := by { dsimp, rw [id_comp, h.comm‚ÇÉ], },
  comm‚ÇÉ' := h.comm‚ÇÑ, }

/- TODO (@joelriou): show that in order to verify the existence of an octahedron, one may
replace the composable maps `u‚ÇÅ‚ÇÇ` and `u‚ÇÇ‚ÇÉ` by any isomorphic composable maps
and the given "cones" of `u‚ÇÅ‚ÇÇ`, `u‚ÇÇ‚ÇÉ`, `u‚ÇÅ‚ÇÉ` by any choice of cones. -/

end octahedron

end triangulated

open triangulated

variable (C)

/-- A triangulated category is a pretriangulated category which satisfies
the octahedron axiom (TR 4), see https://stacks.math.columbia.edu/tag/05QK -/
class is_triangulated :=
(octahedron_axiom : ‚àÄ ‚¶ÉX‚ÇÅ X‚ÇÇ X‚ÇÉ Z‚ÇÅ‚ÇÇ Z‚ÇÇ‚ÇÉ Z‚ÇÅ‚ÇÉ : C‚¶Ñ ‚¶Éu‚ÇÅ‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ‚¶Ñ ‚¶Éu‚ÇÇ‚ÇÉ : X‚ÇÇ ‚ü∂ X‚ÇÉ‚¶Ñ ‚¶Éu‚ÇÅ‚ÇÉ : X‚ÇÅ ‚ü∂ X‚ÇÉ‚¶Ñ
  (comm : u‚ÇÅ‚ÇÇ ‚â´ u‚ÇÇ‚ÇÉ = u‚ÇÅ‚ÇÉ)
  ‚¶Év‚ÇÅ‚ÇÇ : X‚ÇÇ ‚ü∂ Z‚ÇÅ‚ÇÇ‚¶Ñ ‚¶Éw‚ÇÅ‚ÇÇ : Z‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ü¶1‚üß‚¶Ñ (h‚ÇÅ‚ÇÇ : triangle.mk u‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ w‚ÇÅ‚ÇÇ ‚àà dist_triang C)
  ‚¶Év‚ÇÇ‚ÇÉ : X‚ÇÉ ‚ü∂ Z‚ÇÇ‚ÇÉ‚¶Ñ ‚¶Éw‚ÇÇ‚ÇÉ : Z‚ÇÇ‚ÇÉ ‚ü∂ X‚ÇÇ‚ü¶1‚üß‚¶Ñ (h‚ÇÇ‚ÇÉ : triangle.mk u‚ÇÇ‚ÇÉ v‚ÇÇ‚ÇÉ w‚ÇÇ‚ÇÉ ‚àà dist_triang C)
  ‚¶Év‚ÇÅ‚ÇÉ : X‚ÇÉ ‚ü∂ Z‚ÇÅ‚ÇÉ‚¶Ñ ‚¶Éw‚ÇÅ‚ÇÉ : Z‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶1‚üß‚¶Ñ (h‚ÇÅ‚ÇÉ : triangle.mk u‚ÇÅ‚ÇÉ v‚ÇÅ‚ÇÉ w‚ÇÅ‚ÇÉ ‚àà dist_triang C),
  nonempty (octahedron comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ))

namespace triangulated

variable {C}

/-- A choice of octahedron given by the octahedron axiom. -/
def some_octahedron [is_triangulated C] : octahedron comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ :=
(is_triangulated.octahedron_axiom comm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ).some

end triangulated

end category_theory
